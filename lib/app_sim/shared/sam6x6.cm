# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    sam6x6.cm, version p
# 
# AUTHOR:
#    Bob Chamberlain
#    Dave Hanks
#
# DESCRIPTION:
#    This cell model computes shape parameters for the six-sector CGE 
#    Athena Economics Model from SAM input. It is intended to be run before 
#    eco6x6.cm, either stand-alone or from within Athena, perhaps as part
#    of the eco6x6.cm model's null page.
#
#    There is also a cell model for calibration of actor parameters for 
#    use in actor management.
#
# INPUT: SAM data, prices, REM, and NR.black for a base case.
#
# OUTPUT: * CGE shape parameters and sector-by-sector demands
#         * Sector revenues & expenses
#         * Production & export quantities
#         * FAA & FAR for the base case
#         * Base case per capita demand for goods
#
# PAGES:
#    The model consists of a single page. 
#
# HISTORY:
#    Version p: Added computation of the BaseGDP. 
#               Added BaseSubsisters and BaseSA to the base case 
#               description
#
#    Version o: Changed the default for BREM and BaseConsumers. The CGE
#               was getting negative GDP with the old defaults since the
#               GDP calculation changed in it.
#
#    Version n: Changed A.goods.pop to BA.goods.pop, the base per capita
#               demand for goods. This will be the starting point for
#               the CGE but A.goods.pop may change over time depending on
#               the number of consumers and remittances.
#
#    Version m: Added base subsistence wage to be passed through to the
#               CGE.
#
#    Version l: Zeroed out the actors sector, it is going to get computed
#               by Athena when the scenario is locked.
#
#    Version k: Added some bullet proofing in case the actors defined
#               in Athena have no income.
#
#    Version j: Deleted a default for f.pop.black, it's not needed.
#
#    Version i: Revised the equations for f.i.j to allow for sector 
#               revenues beyond those of the "simplified" SAM.  In 
#               particular, the black sector loses both its cost of 
#               feedstock and its net revenues, the populace gets REM,
#               the actors get the black market net revenues and the
#               graft portion of foreign aid to the rest of the region,
#               the region sector loses that graft, and the world gets
#               the cost of black feedstock and loses REM.  Normalization
#               of the f.i.j.
#
#    Version h: Updated the default BX.i.j values as provided by Bob. Also
#               bullet proofed some of the equations to prevent divide by
#               zero problems should money flows through the black sector
#               stop.
#
#    Version g: Added a cell for BaseConsumers. This represents the number
#               of consumers on which the data in the SAM is based. It is
#               used to compute A.goods.pop which is used by the CGE
#
#    Version f: Changed the name of the file to sam6x6.cm
#
#    Version e: Changed default values for the BX.i.j to the ones found
#               in the most recent version of the econ paper.
#
#               Normalized f.goods.goods and f.pop.goods to 1.0
#               Normalized f.goods.pop, f.black.pop and f.pop.pop to 1.0
#
#    Version d: Simplified the SAM definition.
#               BX.ab in the SAM does not include BNR.b.
#                X.ab in the CGE does include NR.b.
#               BX.aw in the SAM does not include graft*FAR.
#                X.aw in the CGE does include graft*FAR.
#               BX.rw in the SAM has all of FAR.
#                X.rw in the CGE has lost graft*FAR.
#               BX.pw in the SAM does not include BREM.
#                X.pw in the CGE does include REM.
#               BX.wb in the SAM does not include the cost of feedstocks:
#                   PF.wb * AF.wb * BQD.b
#                X.wb in the CGE does include the cost of feedstocks:
#                   PF.wb * AF.wb * QD.b
#
#    Version c: Fixed an equation that caused the cell model to act
#               cyclically. Updated SAM inputs from the latest 6 sector
#               tableau. 
#
#    Version b: Model is sane (passes cmtool "check"). Initial SAM values
#               are included, cleaned up the syntax and added price of
#               black market feedstock and feedstock required from world
#
#    Version a: Initial version, based on the calibration equations in 
#               Section 4.5.i.6 (where i = the sectors of the model) of the
#               CGE derivation document, Athena's Computable General
#               Equilibrium Model by Robert G. Chamberlain and William H.
#               Duquette.
#
#=======================================================================
#
# The required input data is:
#
#   BX.i.j     The payment in $/year from sector j to sector i. The indices
#              i and j take the values from the set (black goods pop actors 
#              region world). Strictly speaking, this and only this is 
#              the social accounting matrix (SAM).
#
#   BP.j       The price, in $/unit of the product of sector j, where j 
#              takes values only for the sectors with products: (black, 
#              goods, pop).
#
#   BREM       Payments from outside the region directly to the populace, 
#              consisting of remittances in $/year. 
#
#   BNR.black  Net revenue for the black sector, in $/year.
#
#   AF.i.j     Amount of feedstock needed per unit of product. In this SAM
#              this is used only for i=world and j=black
#
#-----------------------------------------------------------------------
#
# Indices

index i   {goods black pop actors region world} ;# all receiving sectors
index j   {goods black pop actors region world} ;# all paying sectors
index il  {goods black pop}                     ;# sectors in the local 
                                                ;# economy that have product
index inp {actors region world}                 ;# sectors with no product or
                                                 # outside the local economy

# All BX.i.j values are from a SAM.
# SAM cell definitions do not include cash flows of black feedstock costs
# nor of the black sector's net revenue or the graft portion of the FAR
# to the actors sector.

let BX.goods.goods  = 1.087e9
let BX.goods.black  = 3.0e6
let BX.goods.pop    = 96.145e9
let BX.goods.actors = 0.0
let BX.goods.region = 776.0e6
let BX.goods.world  = 23.390e9

let BX.black.goods = 0.0
let BX.black.black = 0.0
let BX.black.pop   = 93.0e6
let BX.black.actors = 0.0
let BX.black.region = 0.0
let BX.black.world  = 1.653e9

let BX.pop.goods    = 97.198e9
let BX.pop.black    = 43.0e6
let BX.pop.pop      = 0.0
let BX.pop.actors   = 0.0
let BX.pop.region   = 302.0e6
let BX.pop.world    = 216.0e6

let BX.actors.goods = 0.0
let BX.actors.black = 0.0
let BX.actors.pop   = 0.0
let BX.actors.actors = 0.0
let BX.actors.region = 0.0
let BX.actors.world  = 0.0

let BX.region.goods  = 0.0
let BX.region.black  = 0.0
let BX.region.pop    = 0.0
let BX.region.actors = 0.0
let BX.region.region = 0.0
let BX.region.world  = 1.079e9

let BX.world.goods   = 18.391e9
let BX.world.black   = 1.222e9
let BX.world.pop     = 11.688e9
let BX.world.actors  = 0.0
let BX.world.region  = 0.0
let BX.world.world   = 0.0


let BP.black = 500000  ;# Base price in the black sector $/tonne
let BP.pop   = 400     ;# Base price in the pop sector $/work-year
let BP.goods = 1       ;# Base price in the goods sector $/goodsBKT
let BP.actors = 0
let BP.region = 0
let BP.world  = 0

let BaseConsumers  = 130.0e6
let BaseSubsisters = 50.0e6
let BaseSubWage    = 200                     ;# Subsistence wage 
                                              # (aka poverty level) $/work-year
let BaseSA         = {[BaseSubsisters] * [BaseSubWage]}
let BaseCAP.pop    = {[BREV.pop] / [BP.pop]} ;# Number of base case jobs

let graft          = 0.3    ;# Fraction of foreign aid to region that 
                             # goes to actors.
let PF.world.black = 62.0e3
let AF.world.black = 1.05
let MF.world.black = 35e3


# Macro definitions

define t.i.j {i j} {[BX.$i.$j] / [BREV.$j]}
define k.j {j} {1.0 - ([t.actors.$j] + [t.region.$j] + [t.world.$j])}
define f.i.j {i j} {[BX.$i.$j] / ([CDEXP.$j])}
define A.i.j {i j} {[BX.$i.$j] / ([BP.$i] * [BQD.$j])}

#-----------------------------------------------------------------------
#
# The sector revenues and expenses are computed by:

foreach i {goods black pop actors region world} {
    # Sector revenues sum the row
    let BREV.$i = {<:sum j {[BX.$i.$j]}:>} 
}

foreach j {goods black pop actors region world} {
    # Sector expenditures sum the column 
    let BEXP.$j = {<:sum i {[BX.$i.$j]}:>} 
}

foreach j {goods black pop} {
    # Sectors with Cobb-Douglas coefficients that must add to 1.0
    let CDEXP.$j = {<:sum il {[BX.$il.$j]}:>}
}

# Sector-by-sector demands for all i and j are given by:
foreach i {goods black pop} {
    foreach j {goods black pop actors region world} {
        let BQD.$i.$j = {[BX.$i.$j]/[BP.$i]}
    }
}

let BREM = {max(0, [BEXP.pop] - [BREV.pop])}

# Production quantities for all producers i are a simple sum of the 
# sector-by-sector demands:

foreach i {goods black pop} {
    let BQD.$i = {<:sum j {[BQD.$i.$j]}:>}
}

let FEED.black = {[PF.world.black]*[AF.world.black]*[BQD.black]}

let BNR.black = {[BREV.black] - ([BEXP.black] + [FEED.black])}

let FAA = {[BX.actors.world]}
let FAR = {[BX.region.world]}

# The allocatable income for each sector is computed by:

let BALLOC.goods  = {[BEXP.goods]}
let BALLOC.black  = {[BEXP.black]}
let BALLOC.pop    = {[BEXP.pop] + [BREM]}
let BALLOC.actors = {[BEXP.actors]}
let BALLOC.region = {[BREV.region] - [graft]*[FAR]}
let BALLOC.world  = {[BREV.world] - [BREM] + [FEED.black]}

# Exports are obtained by:

let EXPORTS.goods = {[BX.goods.world] / [BP.goods]}
let EXPORTS.black = {[BX.black.world] / [BP.black]}
let EXPORTS.pop   = {[BX.pop.world]   / [BP.pop]}

# Finally, the shape parameter equations are:

# For the goods sector
# When, in a later version, the goods sector is split into 
# user-defined goods sectors, we'll need to bulletproof by checking
# for division by zero.

let t.actors.goods = {<:t.i.j actors goods:>}
let t.region.goods = {<:t.i.j region goods:>}
let t.world.goods  = {<:t.i.j world goods:>}
let k.goods = {<:k.j goods:>}

let f.goods.goods =  {<:f.i.j goods goods:>}
let f.black.goods = 0.0  ;# bullet proofing: f.black.goods is not defined
let f.pop.goods   = {<:f.i.j pop goods:>}

# For the black sector
# All values are checked for divide by zero in case money flows through
# the black sector stop.

let A.goods.black = {
    [BQD.black] > 0.0 ? [BX.goods.black] / ([BP.goods] * [BQD.black]) : 0.0
}
let A.black.black = {
    [BQD.black] > 0.0 ? [BX.black.black] / ([BP.black] * [BQD.black]) : 0.0
}

let A.pop.black   = {
    [BQD.black] > 0.0 ? [BX.pop.black] / ([BP.pop] * [BQD.black]) : 0.0
}

let t.actors.black = {
    [BREV.black] > 0.0 ? [BX.actors.black] / [BREV.black] : 0.0
}

let t.region.black = {
    [BREV.black] > 0.0 ? [BX.region.black] / [BREV.black] : 0.0 
}

let t.world.black = {
    [BREV.black] > 0.0 ? [BX.world.black] / [BREV.black] : 0.0
}

# For the pop sector

foreach i {actors region world} {
    let t.$i.pop = {[BX.$i.pop] / [BREV.pop]}
}

# Using BEXP.pop instead of BALLOC.pop because BREM is not subject to
# the taxes imposed on regular income.
let k.pop = {<:k.j pop:>}
let BATI.pop = {[CDEXP.pop]}

foreach i {goods black pop} {
    let f.$i.pop = {[BX.$i.pop] / [BATI.pop]}
}


# For the actors sector

foreach i {goods black pop actors region world} {
    let f.$i.actors = {
        [BALLOC.actors] > 0 ? [BX.$i.actors] / [BALLOC.actors] : 0.0
    }
}

# For the region sector

foreach i {goods black pop actors region world} {
    let f.$i.region = {[BX.$i.region] / [BALLOC.region]}
}

# Base case per capita demand for goods 
let BA.goods.pop = {[BX.goods.pop] / ([BP.goods] * [BaseConsumers])}

# Base case GDP
let BGDP.goods = {
    [BP.goods] * ([BQD.goods.pop]    + [BQD.goods.actors] + 
                  [BQD.goods.region] + [EXPORTS.goods])
}

let BGDP.black = {
    [BP.black] * ([BQD.black.pop]    + [BQD.black.actors] +
                  [BQD.black.region] + [EXPORTS.black])
}

let BGDP.pop = {
    [BP.pop] * ([BQD.pop.pop]    + [BQD.pop.actors] +
                [BQD.pop.region] + [EXPORTS.pop])
}

let BaseGDP = {
    [BGDP.goods] + [BGDP.black] + [BGDP.pop] -
    [BREV.world] +
    [BaseSA] - [FEED.black]
}

# Diagnostics
# TBD




