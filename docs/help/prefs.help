# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    prefs.help
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Athena Help Documentation: User Preferences
#
#    This file is included into athena.help.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------

page prefs "User Preferences" "" {
    <<title>>
    User preferences are user-specific settings that customize
    the behavior of the application.  As such, preference parameters
    affect the user experience, but not the simulation outputs.
    Preference values can be queried and set using the
    <<cmdref prefs>> executive command.<p>

    <<childlinks>>
}

# prefpage parm itemtype
#
# parm     The name of the parameter or subset
# itemtype parm|subset
#
# Defines a parameter help page, defining the parent/child links properly.

proc prefpage {parm itemtype} {
    set tokens [split $parm "."]
    set tag "pref.$parm"

    set parent [join [lrange $tokens 0 end-1] .]

    if {[llength $parent] == 0} {
        set ptag "prefs"
    } else {
        set ptag "pref.$parent"
    }

    set text "<<prefs.header $parm $itemtype>>"
    append text [super ::projectlib::prefs docstring $parm]
    append text "<p>"

    if {$itemtype eq "subset"} {
        append text "\n<<childlinks>>"
    }

    if {$itemtype eq "subset"} {
        page $tag $parm.* $ptag $text
    } else {
        page $tag $parm $ptag $text
    }
}

macro prefs.header {parm itemtype} {
    set tokens [split $parm .]

    set namelinks ""
    
    set cref "pref"

    foreach token [lrange $tokens 0 end-1] {
        append cref ".$token" 
        append namelinks "[cref $cref $token]."
    }

    append namelinks [lindex $tokens end]

    if {$itemtype eq "subset"} {
        append namelinks ".*"
        set defaultValue ""
    } else {
        set defaultValue \
            "Default value: [::projectlib::prefs getdefault $parm]<p>"
    }
} {
    |<--
    [mktitle small "User Preference" $namelinks ""]

    $defaultValue
}

# prefpages
#
# Creates a page called "pref.<parmname>" for each preference parameter.

proc prefpages {} {
    super ::projectlib::prefs init

    foreach {item itemtype} [super ::projectlib::prefs items] {
        prefpage $item $itemtype
    }
}


#-----------------------------------------------------------------------

prefpages

