# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-CONDITIONX-NBMOOD.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    CONDITIONX:NBMOOD:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    set S1 ::strategy::SYSTEM

    proc setup {} {
        ted create NB1
        variable S1
        variable B1
        variable C1
        ted order STRATEGY:BLOCK:ADD agent SYSTEM
        lassign [$S1 blocks] B1
        ted order BLOCK:CONDITION:ADD block_id [$B1 id] typename NBMOOD
        lassign [$B1 conditions] C1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # CONDITIONX:NBMOOD:UPDATE

    test CONDITIONX:NBMOOD:UPDATE-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject CONDITIONX:NBMOOD:UPDATE
    } -cleanup {
        cleanup
    } -result {
        condition_id {required value}
    }

    test CONDITIONX:NBMOOD:UPDATE-1.2 {unknown condition_id} -setup {
        setup
    } -body {
        ted order -reject CONDITIONX:NBMOOD:UPDATE condition_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        condition_id {invalid value "NONESUCH", should be one of: *}
    }

    test CONDITIONX:NBMOOD:UPDATE-1.3 {invalid n} -setup {
        setup
    } -body {
        ted order -reject CONDITIONX:NBMOOD:UPDATE condition_id [$C1 id] n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }

    test CONDITIONX:NBMOOD:UPDATE-1.4 {invalid comp} -setup {
        setup
    } -body {
        ted order -reject CONDITIONX:NBMOOD:UPDATE condition_id [$C1 id] comp NONESUCH
    } -cleanup {
        cleanup
    } -result {
        comp {invalid value "NONESUCH", should be one of: EQ, GE, GT, LE, LT}
    }

    test CONDITIONX:NBMOOD:UPDATE-1.5 {invalid limit} -setup {
        setup
    } -body {
        ted order -reject CONDITIONX:NBMOOD:UPDATE condition_id [$C1 id] limit NONESUCH
    } -cleanup {
        cleanup
    } -result {
        limit {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: VS, S, A, D, VD}
    }

    test CONDITIONX:NBMOOD:UPDATE-2.1 {condition updated} -setup {
        setup
    } -body {
        ted order CONDITIONX:NBMOOD:UPDATE \
            condition_id [$C1 id] \
            n            NB1     \
            comp         gt       \
            limit        10

        ted pdict [$C1 getdict]
    } -cleanup {
        cleanup
    } -match glob -result {
        metflag {}
        id      *
        state   normal
        n       NB1
        block   ::bean::block2
        limit   10
        comp    GT
    }

    test CONDITIONX:NBMOOD:UPDATE-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order CONDITIONX:NBMOOD:UPDATE condition_id [$C1 id] n NB1
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test CONDITIONX:NBMOOD:UPDATE-2.3 {undo notifier event} -setup {
        setup
        ted order CONDITIONX:NBMOOD:UPDATE condition_id [$C1 id] n NB1
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::




