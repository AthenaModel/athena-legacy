# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-ATTRIT.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) ATTRIT:* order tests.
#
#    This test suite tests the attrition-related orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }


    #-------------------------------------------------------------------
    # ATTRIT:UNIT

    test UNIT-1.1 {required parms} -body {
        ted order -reject ATTRIT:UNIT {}
    } -result {
        u {required value}
        casualties {required value}
    }
    

    test UNIT-1.2 {invalid unit} -body {
        ted order -reject ATTRIT:UNIT u NONESUCH casualties 25
    } -result {
        u {Invalid unit name: "NONESUCH"}
    }


    test UNIT-1.3 {invalid casualties} -setup {
        ted create BLUE BLUE1
    } -body {
        ted order -reject ATTRIT:UNIT u BLUE1 casualties NONESUCH
    } -cleanup {
        cleanup
    } -result {
        casualties {invalid value "NONESUCH", expected integer}
    }


    test UNIT-1.4 {negative casualties} -setup {
        ted create BLUE BLUE1
    } -body {
        ted order -reject ATTRIT:UNIT u BLUE1 casualties -1
    } -cleanup {
        cleanup
    } -result {
        casualties {invalid value "-1", expected integer no less than 0}
    }


    test UNIT-2.1 {unit is attrited} -setup {
        ted create NB1 SHIA NB1SHIA NB1SHIA1
    } -body {
        ted order ATTRIT:UNIT u NB1SHIA1 casualties 5
        ted query {SELECT u, personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u        personnel 
-------- --------- 
NB1SHIA1 10        
    }


    test UNIT-2.2 {demographics are updated} -setup {
        ted create NB1 SHIA NB1SHIA NB1SHIA1
    } -body {
        ted order ATTRIT:UNIT u NB1SHIA1 casualties 5
        ted query {SELECT * FROM demog_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    attrition explicit displaced implicit population 
--- ---- --------- -------- --------- -------- ---------- 
NB1 SHIA 5         10       10        985      985        
    }

    test UNIT-3.1 {undo undoes the change} -setup {
        ted create NB1 NB1SHIA NB1SHIA1
        ted order ATTRIT:UNIT u NB1SHIA1 casualties 5
    } -body {
        ted notifier bind ::unit <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::unit <Entity> update NB1SHIA1}
    }


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





