# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-vrel.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    vrel(sim) tests.
#
#    This test suite tests the vrel(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {override is created} -setup {
        ted create JOE SHIA
    } -body {
        vrel mutate create {id {SHIA JOE} vrel 0.5}
        ted query {SELECT * FROM vrel_ga}
    } -cleanup {
        cleanup
    } -result {
g    a   vrel 
---- --- ---- 
SHIA JOE 0.5  
    }

    test create-2.1 {undo deletes the override} -setup {
        ted create JOE SHIA
        set undo [vrel mutate create {id {SHIA JOE} vrel 0.5}]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT * FROM vrel_ga}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Record is deleted} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.5}
    } -body {
        vrel mutate delete {SHIA JOE}

        # Is SHIA gone?
        rdb eval {SELECT g,a FROM vrel_ga}
    } -cleanup {
        cleanup
    } -result {}

    test delete-2.1 {Undo restores record} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.5}
        set undo [vrel mutate delete {SHIA JOE}]
    } -body {
        namespace eval :: $undo
        ted query {SELECT * FROM vrel_ga}
    } -cleanup {
        cleanup
    } -result {
g    a   vrel 
---- --- ---- 
SHIA JOE 0.5  
    }


    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {rel is updated} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.5}
    } -body {
        vrel mutate update {
            id   {SHIA JOE}
            vrel  0.7
        }

        ted query {SELECT * FROM vrel_ga ORDER BY g,a}
    } -cleanup {
        cleanup
    } -result {
g    a   vrel 
---- --- ---- 
SHIA JOE 0.7  
    }


    test update-1.2 {identical values are OK} -setup {
        ted create JOE SHIA

        set parmdict {
            id   {SHIA JOE}
            vrel  0.5
        }

        vrel mutate create $parmdict
    } -body {
        set a [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]
        
        vrel mutate update $parmdict

        set b [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.5}
    } -body {
        set a [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]

        vrel mutate update  {
            id   {SHIA JOE}
            vrel  ""
        }

        set b [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.0}
    } -body {

        set a [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]

        set undo [vrel mutate update {
            id   {SHIA JOE}
            vrel  0.5
        }]

        eval $undo

        set b [ted query {SELECT * FROM vrel_ga ORDER BY g,a}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # exists

    test exists-1.1 {groups do exist} -body {
        vrel exists {NONESUCH NONESUCH}
    } -cleanup {
        cleanup
    } -result {0}


    test exists-1.2 {record does not exist} -body {
        ted create JOE SHIA
        vrel exists {SHIA JOE}
    } -cleanup {
        cleanup
    } -result {0}

    test exists-1.3 {record exists} -setup {
        ted create JOE SHIA
        vrel mutate create {id {SHIA JOE} vrel 0.5}
    } -body {
        vrel exists {SHIA JOE}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid group f} -body {
        vrel validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -result {Invalid group, none are defined}


    test validate-1.2 {Invalid group g} -setup {
        ted create JOE SHIA
    } -body {
        vrel validate {NONESUCH JOE}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid group, should be one of: SHIA}

    test validate-1.3 {Invalid actor a} -setup {
        ted create JOE SHIA
    } -body {
        vrel validate {SHIA NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid actor, should be one of: JOE}


    test validate-2.1 {Valid relationship} -setup {
        ted create JOE SHIA
    } -body {
        vrel validate {SHIA JOE}
    } -cleanup {
        cleanup
    } -result {SHIA JOE}

    #-------------------------------------------------------------------
    # vrel_view
    #
    # The vrel_view is implemented in scenariodb.sql, not in vrel.tcl,
    # so perhaps it doesn't belong here; on the other hand, it pulls
    # together affinities from the bsystem with relationship overrides
    # from vrel_ga, and it ought to be tested somewhere.

    test vrel_view-1.1 {Defaults to 1.0 for owned groups} -setup {
        ted create JOE BLUE SHIA
    } -body {
        ted query {SELECT * FROM vrel_view ORDER BY g,a}
    } -cleanup {
        cleanup
    } -result {
g    gtype a   vrel_nat vrel override 
---- ----- --- -------- ---- -------- 
BLUE FRC   JOE 1.0      1.0  0        
SHIA CIV   JOE 0.0      0.0  0        
    }

    test vrel_view-1.2 {vrel_ga overrides bsystem values} -setup {
        ted create JOE BLUE SHIA
    } -body {
        vrel mutate create {id {SHIA JOE} vrel 0.5}
        vrel mutate create {id {BLUE JOE} vrel 0.25}
        ted query {SELECT * FROM vrel_view ORDER BY g,a}
    } -cleanup {
        cleanup
    } -result {
g    gtype a   vrel_nat vrel override 
---- ----- --- -------- ---- -------- 
BLUE FRC   JOE 1.0      0.25 1        
SHIA CIV   JOE 0.0      0.5  1        
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





