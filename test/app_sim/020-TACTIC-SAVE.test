# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-SAVE.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) TACTIC:* order tests.
#
#    This test suite tests the TACTIC:SAVE:* orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted create JOE
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard items

    set SAVE1 {
        owner       JOE
        priority    bottom
        int1        10
    }

    #-------------------------------------------------------------------
    # TACTIC:SAVE:CREATE

    test SAVE:CREATE-1.1 {required parms} -body {
        ted order -reject TACTIC:SAVE:CREATE {}
    } -result {
        owner {required value}
        int1 {required value}
    }

    
    test SAVE:CREATE-1.2 {a is unknown} -setup {
        setup
    } -body {
        ted order -reject TACTIC:SAVE:CREATE \
            [dict merge $SAVE1 [list owner NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        owner {Invalid actor, should be one of: JOE}
    }

    test SAVE:CREATE-1.3 {int1 is invalid} -setup {
        setup
    } -body {
        ted order -reject TACTIC:SAVE:CREATE \
            [dict merge $SAVE1 [list int1 NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        int1 {invalid value "NONESUCH", expected integer}
    }

    test SAVE:CREATE-1.4 {priority is invalid} -setup {
        setup
    } -body {
        ted order -reject TACTIC:SAVE:CREATE [dict merge $SAVE1 {
            priority NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        priority {invalid value "nonesuch", should be one of: top, bottom}
    }

    test SAVE:CREATE-2.1 {tactic is created} -setup {
        setup
    } -body {
        ted order TACTIC:SAVE:CREATE $SAVE1

        ted querylist {SELECT * FROM tactics_SAVE}
    } -cleanup {
        cleanup
    } -result {
tactic_id    1
tactic_type  SAVE
owner        JOE
narrative    Save 10% of income for later use
priority     1
state        normal
exec_ts      
exec_flag    0
int1         10
    }

    test SAVE:CREATE-2.2 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <tactics>
        ted order TACTIC:SAVE:CREATE $SAVE1
        ted notifier received

        # NOTE: Two updates, because of priority setting.
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
        {::rdb <tactics> update 1}
    }


    test SAVE:CREATE-3.1 {undo effects} -setup {
        setup
        ted order TACTIC:SAVE:CREATE $SAVE1
    } -body {
        ted notifier bind ::rdb <tactics>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> delete 1}
    }


    #-------------------------------------------------------------------
    # TACTIC:SAVE:UPDATE

    proc setup_update_savemoney {} {
        variable SAVE1
        setup
        ted order TACTIC:SAVE:CREATE $SAVE1
    }

    test SAVE:UPDATE-1.1 {required parms} -body {
        ted order -reject TACTIC:SAVE:UPDATE {}
    } -result {
        tactic_id {required value}
        int1 {required value}
    }

    
    test SAVE:UPDATE-1.2 {tactic_id is unknown} -setup {
        setup_update_savemoney
    } -body {
        ted order -reject TACTIC:SAVE:UPDATE tactic_id NONESUCH int1 10
    } -cleanup {
        cleanup
    } -result {
        tactic_id {Invalid tactic ID: "NONESUCH"}
    }

    test SAVE:UPDATE-1.3 {int1 is invalid} -setup {
        setup_update_savemoney
    } -body {
        ted order -reject TACTIC:SAVE:UPDATE tactic_id 1 int1 NONESUCH
    } -cleanup {
        cleanup
    } -result {
        int1 {invalid value "NONESUCH", expected integer}
    }

    test SAVE:UPDATE-1.4 {wrong flavor of tactic} -setup {
        setup_update_savemoney
        rdb eval {UPDATE tactics SET tactic_type='BOGUS'}
    } -body {
        ted order -reject TACTIC:SAVE:UPDATE tactic_id 1 int1 5
    } -cleanup {
        cleanup
    } -result {
        tactic_id {Tactic 1 is not a SAVE tactic}
    }

    test SAVE:UPDATE-2.1 {activities are updated} -setup {
        setup_update_savemoney
    } -body {
        ted order TACTIC:SAVE:UPDATE tactic_id 1 int1 20

        ted querylist {SELECT * FROM tactics_SAVE}
    } -cleanup {
        cleanup
    } -result {
tactic_id    1
tactic_type  SAVE
owner        JOE
narrative    Save 20% of income for later use
priority     1
state        normal
exec_ts      
exec_flag    0
int1         20
    }


    test SAVE:UPDATE-2.2 {Effects} -setup {
        setup_update_savemoney
    } -body {
        ted notifier bind ::rdb <tactics>
        ted order TACTIC:SAVE:UPDATE tactic_id 1 int1 20
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
        {::rdb <tactics> update 1}
    }


    test SAVE:UPDATE-3.1 {undo effects} -setup {
        setup_update_savemoney
        ted order TACTIC:SAVE:UPDATE tactic_id 1 int1 20
    } -body {
        ted notifier bind ::rdb <tactics>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






