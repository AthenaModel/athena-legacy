# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-coffer.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    coffer(sim) tests.
#
#    This test suite tests the coffer(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BOB NB1 NB2 BLUE BRIT SHIA SUNN KURD
        personnel start
        personnel load
        cash load
    }

    # Clean up after a test

    proc cleanup {} {
        strategy locking 0
        ted cleanup
        catch {coff destroy}
    }

    #-------------------------------------------------------------------
    # constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        coffer create coff
        coff getdict
    } -cleanup {
        cleanup
    } -result {cash 0.0 reserve 0.0 troops {}}

    test constructor-1.2 {create with actor} -setup {
        setup
    } -body {
        coffer create coff JOE
        ted pdict [coff getdict]
    } -cleanup {
        cleanup
    } -result {
        cash    10000.0
        reserve 200000.0
        troops  {BLUE {undeployed 5000} BRIT {undeployed 5000}}
    }

    #-------------------------------------------------------------------
    # getdict

    # Tested in use by the other tests.

    #-------------------------------------------------------------------
    # setdict

    test setdict-1.1 {restores state} -setup {
        setup
    } -body {
        coffer create coff
        coff setdict {cash 1 reserve 2 troops DUMMY}
        coff getdict
    } -cleanup {
        cleanup
    } -result {cash 1 reserve 2 troops DUMMY}

    #-------------------------------------------------------------------
    # cash

    test cash-1.1 {retrieves cash-on-hand} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff cash
    } -cleanup {
        cleanup
    } -result {10000.0}

    #-------------------------------------------------------------------
    # reserve

    test reserve-1.1 {retrieves cash-reserve} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff reserve
    } -cleanup {
        cleanup
    } -result {200000.0}

    
    #-------------------------------------------------------------------
    # troops

    test troops-1.1 {retrieves troop levels} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff troops BLUE undeployed
    } -cleanup {
        cleanup
    } -result {5000}

    test troops-1.2 {0 if no troops are in location} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff troops BLUE NB1
    } -cleanup {
        cleanup
    } -result {0}
    
    #-------------------------------------------------------------------
    # spend

    test spend-1.1 {deducts cash from cash-on-hand} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff spend 100.0
        coff cash
    } -cleanup {
        cleanup
    } -result {9900.0}

    test spend-1.2 {insufficient cash on tick} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff spend [expr {[coff cash] + 10.0}]
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {insufficient cash}

    test spend-1.3 {insufficient cash on lock} -setup {
        setup
        strategy locking 1
    } -body {
        # No error on lock, but balance doesn't go negative.
        coffer create coff JOE
        coff spend [expr {[coff cash] + 10.0}]
        coff cash
    } -cleanup {
        cleanup
    } -result {0.0}

    #-------------------------------------------------------------------
    # deposit

    test deposit-1.1 {transfers cash-on-hand to reserve} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff deposit 100.0
        list [coff cash] [coff reserve]
    } -cleanup {
        cleanup
    } -result {9900.0 200100.0}

    test deposit-1.2 {insufficient cash} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff deposit 10001.0
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {insufficient cash}

    #-------------------------------------------------------------------
    # withdraw

    test withdraw-1.1 {transfers reserve to cash-on-hand} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff withdraw 100.0
        list [coff cash] [coff reserve]
    } -cleanup {
        cleanup
    } -result {10100.0 199900.0}

    test withdraw-1.2 {reserve can be negative} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff withdraw 400000.0
        list [coff cash] [coff reserve]
    } -cleanup {
        cleanup
    } -result {410000.0 -200000.0}
    
    #-------------------------------------------------------------------
    # demobilize

    test demobilize-1.1 {removes personnel from undeployed} -setup {
        setup
    } -body {
        coffer create coff JOE
        set a [coff troops BLUE undeployed]
        coff demobilize BLUE 100
        set b [coff troops BLUE undeployed]
        list $a $b
    } -cleanup {
        cleanup
    } -result {5000 4900}
    
    test demobilize-1.2 {insufficient personnel} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff demobilize BLUE 6000
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {insufficient personnel}

    test demobilize-1.3 {can demobilize all} -setup {
        setup
    } -body {
        coffer create coff JOE
        set a [coff troops BLUE undeployed]
        coff demobilize BLUE $a
        set b [coff troops BLUE undeployed]
        list $a $b
    } -cleanup {
        cleanup
    } -result {5000 0}
    

    #-------------------------------------------------------------------
    # mobilize

    test mobilize-1.1 {adds personnel to undeployed} -setup {
        setup
    } -body {
        coffer create coff JOE
        set a [coff troops BLUE undeployed]
        coff mobilize BLUE 100
        set b [coff troops BLUE undeployed]
        list $a $b
    } -cleanup {
        cleanup
    } -result {5000 5100}

    #-------------------------------------------------------------------
    # deploy

    test deploy-1.1 {deploys undeployed personnel to neighborhood} -setup {
        setup
    } -body {
        coffer create coff JOE
        set a [coff troops BLUE undeployed]
        set b [coff troops BLUE NB1]
        coff deploy BLUE NB1 100
        set c [coff troops BLUE undeployed]
        set d [coff troops BLUE NB1]
        list $a $b $c $d
    } -cleanup {
        cleanup
    } -result {5000 0 4900 100}

    test deploy-1.2 {insufficient personnel on tick} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff deploy BLUE NB1 [expr {[coff troops BLUE undeployed] + 100}]
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {insufficient personnel}

    test deploy-1.3 {insufficient personnel on lock} -setup {
        setup
        strategy locking 1
    } -body {
        coffer create coff JOE
        coff deploy BLUE NB1 [expr {[coff troops BLUE undeployed] + 100}]
        coff troops BLUE undeployed
    } -cleanup {
        cleanup
    } -result {0}

    #-------------------------------------------------------------------
    # assign

    test assign-1.1 {assigns unassigned personnel in neighborhood} -setup {
        setup
    } -body {
        coffer create coff JOE
        coff deploy BLUE NB1 2500
        set a [coff troops BLUE NB1]
        coff assign BLUE NB1 100
        set b [coff troops BLUE NB1]
        list $a $b
    } -cleanup {
        cleanup
    } -result {2500 2400}

    test assign-1.2 {insufficient personnel on tick} -setup {
        setup
        coffer create coff JOE
        coff deploy BLUE NB1 2500
    } -body {
        coff assign BLUE NB1 [expr {[coff troops BLUE NB1] + 100}]
        coff troops BLUE NB1
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {insufficient personnel}

    test assign-1.3 {insufficient personnel on lock} -setup {
        setup
        strategy locking 1
        coffer create coff JOE
        coff deploy BLUE NB1 2500
    } -body {
        coff assign BLUE NB1 [expr {[coff troops BLUE NB1] + 100}]
        coff troops BLUE NB1
    } -cleanup {
        cleanup
    } -result {0}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::








