# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_spend.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_spend(sim) tests.
#
#    This test suite tests the tactic_spend(sim) tactic ensemble.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE 

        # Verify that unused columns contain NULL
        rdb nullvalue "@"
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        rdb nullvalue ""
        array unset a
        array unset b
        ted cleanup
    }


    # Standard Tactics
    
    set T1 {
        tactic_type SPEND
        owner       JOE
        priority    top
        int1        10
    }

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::SPEND narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Spend 10% of cash reserve}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {check: success} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::SPEND check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {}

    # NOTE: check can't fail for this tactic


    #-------------------------------------------------------------------
    # dollars

    test dollars-1.1 {dollars} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::SPEND dollars [tactic get 1]
    } -cleanup {
        cleanup
    } -result {n/a}


    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {execute: zero reserve} -setup {
        setup
        tactic mutate create $T1
        
        rdb eval { UPDATE actors SET cash_reserve=0.0 }
    } -body {
        array set a [actor get JOE]
        set flag [tactic::SPEND execute [tactic get 1]]
        array set b [actor get JOE]

        list $flag \
            $a(cash_on_hand) $a(cash_reserve) \
            $b(cash_on_hand) $b(cash_reserve)
    } -cleanup {
        cleanup
    } -result {0 0.0 0.0 0.0 0.0}

    test execute-1.2 {execute: ample reserve} -setup {
        setup
        tactic mutate create $T1
    } -body {
        array set a [actor get JOE]
        set flag [tactic::SPEND execute [tactic get 1]]
        array set b [actor get JOE]

        list $flag \
            $a(cash_on_hand) $a(cash_reserve) \
            $b(cash_on_hand) $b(cash_reserve)
    } -cleanup {
        cleanup
    } -result {1 0.0 200000.0 20000.0 180000.0}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






