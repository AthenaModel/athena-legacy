# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-gofer_civgroups.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    civgroup(sim) tests.
#
#    This test suite tests the gofer::CIVGROUPS type.
#
#    NOTE: The type itself is implemented using gofer(n); thus,
#    we don't need to test the behavior of the type itself.  As a result,
#    this test suite focusses on the gofer::CIVGROUPS-specific rule
#    objects.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.
namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    proc setup {} {
        ted create JOE BOB BLUE SHIA SUNN KURD
    }

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Helper: validateAnyAllAlist

    test validateAnyAllAlist-3.1 {validate, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::validateAnyAllAlist {anyall any alist {JOE BOB}}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test validateAnyAllAlist-3.2 {validate, invalid anyall} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::validateAnyAllAlist {anyall NONESUCH alist JOE}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -match glob -result {invalid value "NONESUCH", should be one of: ANY, ALL}

    test validateAnyAllAlist-3.3 {validate, invalid alist} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::validateAnyAllAlist {anyall ANY alist {JOE NONESUCH}}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -match glob -result {Invalid actor*}

    #-------------------------------------------------------------------
    # helper: narrativeAnyAllAlist
    

    test narrativeAnyAllAlist-4.1 {narrative, one actor} -body {
        ::gofer::CIVGROUPS::narrativeAnyAllAlist {anyall ANY alist JOE}
    } -result {actor JOE}

    test narrativeAnyAllAlist-4.2 {narrative, any, several actors} -body {
        ::gofer::CIVGROUPS::narrativeAnyAllAlist {
            anyall ANY
            alist {JOE BOB}
        }
    } -result {any of these actors (JOE, BOB)}
    
    test narrativeAnyAllAlist-4.3 {narrative, all, several actors} -body {
        ::gofer::CIVGROUPS::narrativeAnyAllAlist {
            anyall ALL
            alist {JOE BOB}
        }
    } -result {all of these actors (JOE, BOB)}

    test narrativeAnyAllAlist-4.4 {narrative, -brief} -body {
        ::gofer::CIVGROUPS::narrativeAnyAllAlist {
            anyall ANY
            alist {A B C D E F G H I J}
        } -brief
    } -result {any of these actors (A, B, C, D, E, F, G, H, ...)}



    #-------------------------------------------------------------------
    # Rule: BY_VALUE

    test BY_VALUE-1.1 {construct, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::BY_VALUE construct {SUNN SHIA}
    } -cleanup {
        cleanup
    } -result {raw_value {SUNN SHIA}}

    test BY_VALUE-2.1 {validate, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::BY_VALUE validate {raw_value {SUNN SHIA}}
    } -cleanup {
        cleanup
    } -result {raw_value {SUNN SHIA}}

    test BY_VALUE-2.2 {validate, invalid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::BY_VALUE validate {raw_value {SUNN NONESUCH}}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -match glob -result {Invalid civilian group*}

    test BY_VALUE-3.1 {narrative} -body {
        ::gofer::CIVGROUPS::BY_VALUE narrative {raw_value SUNN}
    } -result {group SUNN}

    test BY_VALUE-4.1 {eval} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::BY_VALUE eval {raw_value {SUNN SHIA}}
    } -cleanup {
        cleanup
    } -result {SUNN SHIA}

    #-------------------------------------------------------------------
    # Rule: RESIDENT_IN

    test RESIDENT_IN-1.1 {construct, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::RESIDENT_IN construct {NB1 NB2}
    } -cleanup {
        cleanup
    } -result {nlist {NB1 NB2}}

    test RESIDENT_IN-2.1 {validate, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::RESIDENT_IN validate {nlist {NB1 NB2}}
    } -cleanup {
        cleanup
    } -result {nlist {NB1 NB2}}

    test RESIDENT_IN-2.2 {validate, invalid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::RESIDENT_IN validate {nlist {NB1 NONESUCH}}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -match glob -result {Invalid neighborhood*}

    test RESIDENT_IN-3.1 {narrative} -body {
        ::gofer::CIVGROUPS::RESIDENT_IN narrative {nlist NB1}
    } -result {non-empty civilian groups resident in NB1}

    test RESIDENT_IN-4.1 {eval, valid} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::RESIDENT_IN eval {nlist {NB1 NB2}}
    } -cleanup {
        cleanup
    } -result {SHIA SUNN KURD}


    #-------------------------------------------------------------------
    # Rule: SUPPORTING_ACTOR

    proc setupSupportingActor {} {
        setup
        rdb eval {
            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','SHIA','BOB',0.3);

            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','SHIA','JOE',0.3);

            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','SUNN','BOB',0.3);

            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','SUNN','JOE',0.0);

            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','KURD','BOB',0.0);

            INSERT INTO support_nga(n,g,a,support)
            VALUES('NB1','KURD','JOE',0.3);
        }
    }

    test SUPPORTING_ACTOR-1.1 {construct} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR construct any {JOE BOB}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test SUPPORTING_ACTOR-2.1 {validate} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR validate {anyall any alist {JOE BOB}}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test SUPPORTING_ACTOR-3.1 {narrative} -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR narrative {anyall ANY alist JOE}
    } -match glob -result {civilian groups that actively support *}

    test SUPPORTING_ACTOR-4.1 {eval, one} -setup {
        setupSupportingActor
    } -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR eval {anyall ANY alist BOB}
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}

    test SUPPORTING_ACTOR-4.2 {eval, any of two} -setup {
        setupSupportingActor
    } -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR eval {anyall ANY alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {KURD SHIA SUNN}

    test SUPPORTING_ACTOR-4.3 {eval, all of two} -setup {
        setupSupportingActor
    } -body {
        ::gofer::CIVGROUPS::SUPPORTING_ACTOR eval {anyall ALL alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # Rule: LIKING_ACTOR

    proc setupLikingActor {} {
        setup
        ted lock
        aram vrel bset 1 SHIA BOB  0.3
        aram vrel bset 1 SHIA JOE  0.3
        aram vrel bset 1 SUNN BOB  0.3
        aram vrel bset 1 SUNN JOE  0.0
        aram vrel bset 1 KURD BOB  0.0
        aram vrel bset 1 KURD JOE  0.3
        ted step
    }

    test LIKING_ACTOR-1.1 {construct} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR construct any {JOE BOB}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test LIKING_ACTOR-2.1 {validate} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR validate {anyall any alist {JOE BOB}}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test LIKING_ACTOR-3.1 {narrative} -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR narrative {anyall ANY alist JOE}
    } -match glob -result {civilian groups that like *}

    test LIKING_ACTOR-4.1 {eval, one} -setup {
        setupLikingActor
    } -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR eval {anyall ANY alist BOB}
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}

    test LIKING_ACTOR-4.2 {eval, any of two} -setup {
        setupLikingActor
    } -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR eval {anyall ANY alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {KURD SHIA SUNN}

    test LIKING_ACTOR-4.3 {eval, all of two} -setup {
        setupLikingActor
    } -body {
        ::gofer::CIVGROUPS::LIKING_ACTOR eval {anyall ALL alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # Rule: DISLIKING_ACTOR

    proc setupDislikingActor {} {
        setup
        ted lock
        aram vrel bset 1 SHIA BOB -0.3
        aram vrel bset 1 SHIA JOE -0.3
        aram vrel bset 1 SUNN BOB -0.3
        aram vrel bset 1 SUNN JOE  0.0
        aram vrel bset 1 KURD BOB  0.0
        aram vrel bset 1 KURD JOE -0.3
        ted step
    }

    test DISLIKING_ACTOR-1.1 {construct} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR construct any {JOE BOB}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test DISLIKING_ACTOR-2.1 {validate} -setup {
        setup
    } -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR validate {anyall any alist {JOE BOB}}
    } -cleanup {
        cleanup
    } -result {anyall ANY alist {JOE BOB}}

    test DISLIKING_ACTOR-3.1 {narrative} -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR narrative {anyall ANY alist JOE}
    } -match glob -result {civilian groups that dislike *}

    test DISLIKING_ACTOR-4.1 {eval, one} -setup {
        setupDislikingActor
    } -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR eval {anyall ANY alist BOB}
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}

    test DISLIKING_ACTOR-4.2 {eval, any of two} -setup {
        setupDislikingActor
    } -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR eval {anyall ANY alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {KURD SHIA SUNN}

    test DISLIKING_ACTOR-4.3 {eval, all of two} -setup {
        setupDislikingActor
    } -body {
        ::gofer::CIVGROUPS::DISLIKING_ACTOR eval {anyall ALL alist {BOB JOE}}
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::

