# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-hook.test
#
# AUTHOR:
#    Dave Hanks
#
# DESCRIPTION:
#    hook(sim) tests.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test
    proc cleanup {} {
        ted cleanup
    }

    set h1 [dict create \
                hook_id PUPG                \
                longname "Puppies are good" \
                narrative "Puppies are good"]

    set t1 [dict create \
                hook_id  PUPG \
                topic_id T1   \
                position 0.9]

    set m1 [dict create \
                iom_id M1          \
                longname "IOM One" \
                hook_id PUPG]

    #-----------------------------------------------------------------
    # Semantic hook creation

    test create-1.1 {Hook is created} -setup {
    } -body {
        hook mutate create $h1

        ted querylist {SELECT * FROM hooks}
    } -cleanup {
        cleanup
    } -result {
hook_id    PUPG
longname   Puppies are good
narrative  Puppies are good: No position on any topics
    }

    test create-1.2 {Undo deletes the hook} -setup {
        set undo [hook mutate create $h1]
    } -body {
        {*}$undo
        rdb query {SELECT hook_id FROM hooks}
    } -cleanup {
        cleanup
    } -result {}

    #---------------------------------------------------------------
    # Semantic hook update

    test update-1.1 {Hook is updated} -setup {
        hook mutate create $h1
    } -body {
        set parms(hook_id) [rdb onecolumn {SELECT hook_id FROM hooks}]
        set parms(longname) "Puppies are evil"
        hook mutate update [array get parms]
        rdb onecolumn {SELECT longname FROM hooks}
    } -cleanup {
        cleanup
    } -result {Puppies are evil}

    test update-1.2 {Hook update is undone} -setup {
        hook mutate create $h1
        set parms(hook_id) [rdb onecolumn {SELECT hook_id FROM hooks}]
        set parms(longname) "Puppies are evil"
        set undo [hook mutate update [array get parms]]
    } -body {
        {*}$undo
        rdb onecolumn {SELECT longname FROM hooks}
    } -cleanup {
        cleanup
    } -result {Puppies are good}

    #--------------------------------------------------------------
    # Semantic hook delete

    test delete-1.1 {Hook is deleted} -setup {
        hook mutate create $h1
    } -body {
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        hook mutate delete $hook_id
        rdb query {SELECT * FROM hooks}
    } -cleanup {
        cleanup
    } -result {}

    test delete-1.2 {Hook deletion is undone} -setup {
        hook mutate create $h1
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        set undo [hook mutate delete $hook_id]
    } -body {
        {*}$undo
        ted querylist {SELECT * FROM hooks}
    } -cleanup {
        cleanup
    } -result {
hook_id    PUPG
longname   Puppies are good
narrative  Puppies are good: No position on any topics
    }

    test delete-1.3 {IOM reflects hook delete} -setup {
        hook mutate create $h1
        iom mutate create $m1
    } -body {
        set a [ted querylist {SELECT * FROM ioms}]
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        hook mutate delete $hook_id
        set b [ted querylist {SELECT * FROM ioms}]
        return $a$b
    } -cleanup {
        cleanup
    } -result {
iom_id    M1
longname  IOM One
hook_id   PUPG
    
iom_id    M1
longname  IOM One
hook_id   
    }

    test delete-1.4 {Undo restores hook in ioms} -setup {
        hook mutate create $h1
        iom mutate create $m1
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        set undo [hook mutate delete $hook_id]
    } -body {
        namespace eval :: $undo
        expr {[rdb onecolumn {SELECT hook_id FROM ioms}] == $hook_id}
    } -cleanup {
        cleanup
    } -result {1}
        
    #---------------------------------------------------------------
    # Semantic hook topic create

    test createtopic-1.1 {Hook topic is created} -setup {
        bsystem topic add T1
        hook mutate create $h1
    } -body {
        hook mutate topic create $t1
        ted querylist {SELECT * FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {
hook_id    PUPG
topic_id   T1
narrative  Passionately For TBD
state      normal
position   0.9
    }

    test createtopic-1.2 {Undo deletes the hook topic} -setup {
        bsystem topic add T1
        hook mutate create $h1
        set undo [hook mutate topic create $t1]
    } -body {
        {*}$undo
        rdb eval {SELECT id FROM hook_topics_view}
    } -cleanup {
        cleanup
    } -result {}

    #---------------------------------------------------------------
    # Semantic hook topic update

    test updatetopic-1.1 {Hook topic is updated} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set parms(id) [rdb onecolumn {SELECT id FROM hook_topics_view}]
        set parms(position) 0.0
        hook mutate topic update [array get parms]
        rdb onecolumn {SELECT position FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {0.0}

    test updatetopic-1.2 {Hook topic update is undone} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
        set parms(id) [rdb onecolumn {SELECT id FROM hook_topics_view}]
        set parms(position) 0.0
        set undo [hook mutate topic update [array get parms]]
    } -body {
        {*}$undo
        rdb onecolumn {SELECT position FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {0.9}

    #--------------------------------------------------------------
    # Semantic hook topic state change

    test state-1.1 {Hook is changed to disabled} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        set state "disabled"
        hook mutate topic state $id $state
        rdb onecolumn {SELECT state FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {disabled}

    test state-1.2 {Undo hook is changed to disabled} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        set state "disabled"
        set undo [hook mutate topic state $id $state]
    } -body {
        {*}$undo
        rdb onecolumn {SELECT state FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {normal}


    #--------------------------------------------------------------
    # Semantic hook topic delete

    test deletetopic-1.1 {Hook topic is deleted} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        hook mutate topic delete $id
        rdb query {SELECT * FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {}

    test delete-1.2 {Hook deletion is undone} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        set undo [hook mutate topic delete $id]
    } -body {
        {*}$undo
        ted querylist {SELECT * FROM hook_topics}
    } -cleanup {
        cleanup
    } -result {
hook_id    PUPG
topic_id   T1
narrative  Passionately For TBD
state      normal
position   0.9
    }

    #----------------------------------------------------------
    # Sanity check

    test sanity-1.1 {Hook module is sane} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        hook sanity check
    } -cleanup {
        cleanup
    } -result {1}

    test sanity-1.2 {Hook module is not sane} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        bsystem topic delete T1
        hook sanity check
    } -cleanup {
        cleanup
    } -result {0}

    #----------------------------------------------------------
    # Public methods

    # names
    test names-1.1 {Hook module returns short names} -setup {
        hook mutate create $h1
    } -body {
        set names [rdb onecolumn {SELECT hook_id FROM hooks}]
        expr {[hook names] eq $names}
    } -cleanup {
        cleanup
    } -result {1}

    # get
    test get-1.1 {Get all hook data} -setup {
        hook mutate create $h1
    } -body {
        set id [rdb onecolumn {SELECT hook_id FROM hooks}]
        hook get $id
    } -cleanup {
        cleanup
    } -result {narrative {Puppies are good: No position on any topics} longname {Puppies are good} hook_id PUPG}

    test get-1.2 {Get an attribute} -setup {
        hook mutate create $h1
    } -body {
        hook get PUPG "longname"
    } -cleanup {
        cleanup
    } -result {Puppies are good}

    # validate
    test validate-1.1 {Hook is valid} -setup {
        hook mutate create $h1
    } -body {
        set id [rdb onecolumn {SELECT hook_id FROM hooks}]
        expr {[hook validate $id] == $id}
    } -cleanup {
        cleanup
    } -result {1}
    
    test validate-1.2 {Hook is invalid} -setup {
        hook mutate create $h1
    } -body {
        hook validate "FOO"
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid hook ID, should be one of: PUPG}
        
    test validate-1.3 {Hook topic is valid} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        expr {[hook topic validate $id] == $id}
    } -cleanup {
        cleanup
    } -result {1}

    test validate-1.4 {Hook topic is invalid; hook id} -setup {
    } -body {
        hook topic validate {FOO BAR}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid hook ID, none are defined}

    test validate-1.5 {Hook topic is invalid; topic id} -setup {
        hook mutate create $h1
    } -body {
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        hook topic validate [list $hook_id FOO]
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid topic, none are defined}

    # topic exists
    test topic_exists-1.1 {Hook topic exists} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        hook topic exists $id
    } -cleanup {
        cleanup
    } -result {1}

    test topic_exists-1.2 {Hook topic does not exist} -setup {
    } -body {
        hook topic exists {FOO BAR}
    } -result {0}

    # topic get
    test topic_get-1.1 {Get all data} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        hook topic get $id
    } -cleanup {
        cleanup
    } -result {narrative {Passionately For TBD} position 0.9 state normal hook_id PUPG topic_id T1}

    test topic_get-1.2 {Get one field} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set id [rdb onecolumn {SELECT id FROM hook_topics_view}]
        hook topic get $id "position"
    } -cleanup {
        cleanup
    } -result {0.9}


    # getdict
    test getdict-1.1 {Nothing is returned} -setup {
    } -body {
        hook getdict FOO
    } -result {}

    test getdict-1.2 {dict is returned} -setup {
        bsystem topic add T1
        hook mutate create $h1
        hook mutate topic create $t1
    } -body {
        set hook_id [rdb onecolumn {SELECT hook_id FROM hooks}]
        hook getdict $hook_id
    } -cleanup {
        cleanup
    } -result {T1 0.9}


    cleanupTests
}

namespace delete ::athena_test::tests::

