# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-frcgroup.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    frcgroup(sim) tests.
#
#    This test suite tests the frcgroup(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    proc mkRoes {} {
        attroe mutate create {
            id        {NB1 BLUE ALQ}
            uniformed 1
            roe       ATTACK
            cooplimit 30
            rate      ""
        }

        attroe mutate create {
            id        {NB1 ALQ BLUE}
            uniformed 0
            roe       ATTACK
            cooplimit 30
            rate      ""
        }
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {frcgroup is created} -body {
        frcgroup mutate create [ted entity BLUE]
        ted query {SELECT * FROM frcgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname color   shape  symbol   demeanor gtype forcetype uniformed local 
---- -------- ------- ------ -------- -------- ----- --------- --------- ----- 
BLUE US Army  #f00001 FRIEND infantry AVERAGE  FRC   REGULAR   1         0     
    }

    test create-1.2 {coop curves are created} -setup {
        ted create SHIA
    } -body {
        frcgroup mutate create [ted entity BLUE]
        rdb eval {SELECT f,g FROM coop_fg ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {SHIA BLUE}

    test create-2.1 {undo deletes the frcgroup} -setup {
        frcgroup mutate create [ted entity BLUE]
        set undo [frcgroup mutate create [ted entity BRIT]]
    } -body {
        {*}$undo
        rdb eval {SELECT g FROM frcgroups_view}
    } -cleanup {
        cleanup
    } -result {BLUE}

    test create-2.2 {undo deletes the coop curves} -setup {
        ted create SHIA
    } -body {
        set undo [frcgroup mutate create [ted entity BLUE]]

        namespace eval :: $undo
        rdb eval {SELECT f,g FROM coop_fg ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {}

    test create-3.1 {personnel_ng curves are created} -setup {
        ted create NB1
    } -body {
        frcgroup mutate create [ted entity BLUE]
        rdb eval {SELECT n,g FROM personnel_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE}

    test create-3.2 {undo deletes the personnel_ng records} -setup {
        ted create NB1
    } -body {
        set undo [frcgroup mutate create [ted entity BLUE]]

        namespace eval :: $undo
        rdb eval {SELECT n,g FROM personnel_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {}

    test create-4.1 {defroe_ng curves are created for uniformed} -setup {
        ted create NB1 ALQ
    } -body {
        frcgroup mutate create [ted entity BLUE]
        rdb eval {SELECT n,g FROM defroe_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE}

    test create-3.2 {undo deletes the personnel_ng records} -setup {
        ted create NB1 ALQ
    } -body {
        set undo [frcgroup mutate create [ted entity BLUE]]

        namespace eval :: $undo
        rdb eval {SELECT n,g FROM defroe_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Group is deleted} -setup {
        ted create BLUE BRIT
    } -body {
        frcgroup mutate delete BLUE

        # Is BLUE gone?
        rdb eval {SELECT g FROM frcgroups_view}
    } -cleanup {
        cleanup
    } -result {BRIT}

    test delete-1.2 {coop curves are deleted} -setup {
        ted create BLUE SHIA
    } -body {
        frcgroup mutate delete BLUE

        rdb eval {SELECT f,g FROM coop_fg ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {}

    test delete-2.1 {Undo restores group} -setup {
        ted create BLUE
        set undo [frcgroup mutate delete BLUE]
    } -body {
        {*}$undo
        ted query {SELECT * FROM frcgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname color   shape  symbol   demeanor gtype forcetype uniformed local 
---- -------- ------- ------ -------- -------- ----- --------- --------- ----- 
BLUE US Army  #f00001 FRIEND infantry AVERAGE  FRC   REGULAR   1         0     
    }

    test delete-2.2 {Undo restores coop curves} -setup {
        ted create BLUE SHIA
        set undo [frcgroup mutate delete BLUE]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT f,g FROM coop_fg ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {SHIA BLUE}

    test delete-3.1 {personnel_ng records are deleted} -setup {
        ted create BLUE NB1
    } -body {
        frcgroup mutate delete BLUE

        rdb eval {SELECT n,g FROM personnel_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {}

    test delete-3.2 {Undo restores personnel_ng} -setup {
        ted create NB1 BLUE
        set undo [frcgroup mutate delete BLUE]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT n,g FROM personnel_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE}

    test delete-4.1 {attroe_nfg records are deleted} -setup {
        ted create NB1 BLUE ALQ
        mkRoes
    } -body {
        frcgroup mutate delete BLUE

        rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}
    } -cleanup {
        cleanup
    } -result {}

    test delete-4.2 {undo restores attroe_nfg records} -setup {
        ted create NB1 BLUE ALQ
        mkRoes

        set undo [frcgroup mutate delete BLUE]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}
    } -cleanup {
        cleanup
    } -result {NB1 ALQ BLUE NB1 BLUE ALQ}


    test delete-5.1 {defroe_ng records are deleted} -setup {
        ted create NB1 BLUE
    } -body {
        frcgroup mutate delete BLUE

        rdb eval {SELECT n,g FROM defroe_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {}

    test delete-5.2 {Undo restores defroe_ng} -setup {
        ted create NB1 BLUE
        set undo [frcgroup mutate delete BLUE]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT n,g FROM defroe_ng ORDER BY n,g}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE}

    test delete-6.1 {calendar items with g are deleted} -setup {
        ted create NB1 BLUE

        activity mutate create {
            n         NB1
            g         BLUE
            a         CURFEW
            tn        NB1
            personnel 70
            start     0
            finish    never
            pattern   daily
            priority  bottom
        }
    } -body {
        frcgroup mutate delete BLUE
        rdb eval {SELECT n,g,a,tn FROM calendar}
    } -cleanup {
        cleanup
    } -result {}

    test delete-6.2 {undoing restores items with g} -setup {
        ted create NB1 BLUE

        activity mutate create {
            n         NB1
            g         BLUE
            a         CURFEW
            tn        NB1
            personnel 70
            start     0
            finish    never
            pattern   daily
            priority  bottom
        }

        set undo [frcgroup mutate delete BLUE]

    } -body {
        namespace eval :: $undo
        rdb eval {SELECT n,g,a,tn FROM calendar}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE CURFEW NB1}


    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {frcgroup is updated} -setup {
        ted create BLUE
    } -body {
        frcgroup mutate update {
            g            BLUE
            longname     "Blue Angels"
            color        "#ff0000"
            shape        NEUTRAL
            forcetype    POLICE
            demeanor     AGGRESSIVE
            uniformed    0
            local        1  
        }

        ted query {SELECT * FROM frcgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname    color   shape   symbol demeanor   gtype forcetype uniformed local 
---- ----------- ------- ------- ------ ---------- ----- --------- --------- ----- 
BLUE Blue Angels #ff0000 NEUTRAL police AGGRESSIVE FRC   POLICE    0         1     
    }


    test update-1.2 {identical values are OK} -setup {
        ted create BLUE
    } -body {
        set a [ted query {SELECT * FROM frcgroups_view}]

        frcgroup mutate update [ted entity BLUE]

        set b [ted query {SELECT * FROM frcgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create BLUE
    } -body {
        set a [ted query {SELECT * FROM frcgroups_view}]

        frcgroup mutate update  {
            g            BLUE
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    ""
            local        ""
        }

        set b [ted query {SELECT * FROM frcgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create BLUE
    } -body {

        set a [ted query {SELECT * FROM frcgroups_view}]

        set undo [frcgroup mutate update {
            g            BLUE
            longname     "Blue Angels"
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    ""
            local        ""
        }]

        {*}$undo

        set b [ted query {SELECT * FROM frcgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    
    test update-3.1 {changing uniformed deletes defroe_ng records} -setup {
        ted create NB1 BLUE
    } -body {
        set a [rdb eval {SELECT n,g FROM defroe_ng}]

        frcgroup mutate update  {
            g            BLUE
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    0
            local        ""
        }

        set b [rdb eval {SELECT n,g FROM defroe_ng}]

        list $a $b
    } -cleanup {
        cleanup
    } -result {{NB1 BLUE} {}}

    test update-3.2 {undo restores defroe_ng records} -setup {
        ted create NB1 BLUE
    } -body {
        set a [rdb eval {SELECT n,g FROM defroe_ng}]

        set undo [frcgroup mutate update  {
            g            BLUE
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    0
            local        ""
        }]

        namespace eval :: $undo

        set b [rdb eval {SELECT n,g FROM defroe_ng}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test update-3.3 {changing uniformed creates defroe_ng records} -setup {
        ted create NB1 ALQ
    } -body {
        set a [rdb eval {SELECT n,g FROM defroe_ng}]

        frcgroup mutate update  {
            g            ALQ
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    1
            local        ""
        }

        set b [rdb eval {SELECT n,g FROM defroe_ng}]

        list $a $b
    } -cleanup {
        cleanup
    } -result {{} {NB1 ALQ}}


    test update-3.4 {undoing change removes defroe_ng records} -setup {
        ted create NB1 ALQ
    } -body {
        set a [rdb eval {SELECT n,g FROM defroe_ng}]

        set undo [frcgroup mutate update  {
            g            ALQ
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    1
            local        ""
        }]

        namespace eval :: $undo

        set b [rdb eval {SELECT n,g FROM defroe_ng}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-3.5 {changing uniformed deletes attroe_nfg records} -setup {
        ted create NB1 BLUE ALQ
        mkRoes
    } -body {
        set a [rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}]

        frcgroup mutate update  {
            g            BLUE
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    0
            local        ""
        }

        set b [rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}]

        list $a $b
    } -cleanup {
        cleanup
    } -result {{NB1 ALQ BLUE NB1 BLUE ALQ} {}}

    test update-3.6 {undoing change restores attroe_nfg records} -setup {
        ted create NB1 BLUE ALQ
        mkRoes
    } -body {
        set a [rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}]

        set undo [frcgroup mutate update  {
            g            BLUE
            longname     ""
            color        ""
            forcetype    ""
            demeanor     ""
            uniformed    0
            local        ""
        }]
        
        namespace eval :: $undo

        set b [rdb eval {SELECT n,f,g FROM attroe_nfg ORDER BY n,f,g}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns names: none defined} -body {
        frcgroup names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns names} -setup {
        ted create BLUE BRIT
    } -body {
        frcgroup names
    } -cleanup {
        cleanup
    } -result {BLUE BRIT}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid name: none defined} -body {
        frcgroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid force group, none are defined}

    test validate-1.2 {Invalid name: some defined} -setup {
        ted create BLUE BRIT
    } -body {
        frcgroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid force group, should be one of: BLUE, BRIT}

    test validate-2.1 {Valid name} -setup {
        ted create BLUE BRIT
    } -body {
        frcgroup validate BRIT
    } -cleanup {
        cleanup
    } -result {BRIT}


    #-------------------------------------------------------------------
    # uniformed names

    test uniformed_names-1.1 {Returns names: none defined} -body {
        frcgroup uniformed names
    } -cleanup {
        cleanup
    } -result {}

    test uniformed_names-1.2 {Returns names} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup uniformed names
    } -cleanup {
        cleanup
    } -result {BLUE BRIT}


    #-------------------------------------------------------------------
    # uniformed validate

    test uniformed_validate-1.1 {Invalid name: none defined} -body {
        frcgroup uniformed validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid uniformed force group, none are defined}

    test uniformed_validate-1.2 {Invalid name: some defined} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup uniformed validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid uniformed force group, should be one of: BLUE, BRIT}

    test uniformed_validate-2.1 {Valid name} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup uniformed validate BRIT
    } -cleanup {
        cleanup
    } -result {BRIT}


    #-------------------------------------------------------------------
    # nonuniformed names

    test nonuniformed_names-1.1 {Returns names: none defined} -body {
        frcgroup nonuniformed names
    } -cleanup {
        cleanup
    } -result {}

    test nonuniformed_names-1.2 {Returns names} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup nonuniformed names
    } -cleanup {
        cleanup
    } -result {ALQ}


    #-------------------------------------------------------------------
    # nonuniformed validate

    test nonuniformed_validate-1.1 {Invalid name: none defined} -body {
        frcgroup nonuniformed validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid non-uniformed force group, none are defined}

    test nonuniformed_validate-1.2 {Invalid name: some defined} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup nonuniformed validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid non-uniformed force group, should be one of: ALQ}

    test nonuniformed_validate-2.1 {Valid name} -setup {
        ted create BLUE BRIT ALQ
    } -body {
        frcgroup nonuniformed validate ALQ
    } -cleanup {
        cleanup
    } -result {ALQ}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





