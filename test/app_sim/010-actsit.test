# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-actsit.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    actsit(sim) tests.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for a test
    proc setup {} {
        ted create JOE NB1 BLUE SHIA
        
        # Turn off auto-demobilization, as it makes setting up 
        # tests difficult.
        parm set strategy.autoDemob no
    }

    # Deploy troops into a neighborhood.
    proc deploy {n g personnel} {
        ted order TACTIC:DEPLOY:CREATE \
            owner JOE g $g text1 SOME int1 $personnel nlist $n
    }

    proc redeploy {n g personnel} {
        rdb eval {
            SELECT tactic_id
            FROM tactics
            WHERE tactic_type = 'DEPLOY'
            AND   nlist=$n
            AND   g=$g
        } {}

        ted order TACTIC:DEPLOY:UPDATE \
            tactic_id $tactic_id int1 $personnel
    }

    proc undeploy {n g} {
        rdb eval {
            SELECT tactic_id
            FROM tactics
            WHERE tactic_type = 'DEPLOY'
            AND   nlist=$n
            AND   g=$g
        } {}

        ted order TACTIC:DELETE \
            tactic_id $tactic_id
    }

    # Clean up after a test
    proc cleanup {} {
        ted cleanup
    }

    ted cleanup

    #-------------------------------------------------------------------
    # table
  
    test table-1.1 {Check return value} -body {
        actsit table
    } -result {actsits_t}

    #-------------------------------------------------------------------
    # analyze

    test analyze-1.1 {Create a situation} -setup {
        setup
        deploy NB1 BLUE 15
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes

        ted query {SELECT * FROM actsits}
    } -cleanup {
        cleanup
    } -result {
s kind     stype    driver n   coverage           state  ts tc change flist g    signature         a        
- -------- -------- ------ --- ------------------ ------ -- -- ------ ----- ---- ----------------- -------- 
1 ::actsit PRESENCE 1      NB1 0.4827181420282134 ACTIVE 0  0  NEW    ALL   BLUE PRESENCE-1-1 0.48 PRESENCE 
    }


    test analyze-1.2 {Change marks are erased on next run} -setup {
        setup
        deploy NB1 BLUE 15
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes
        ted order SIM:RUN days 1 block yes

        ted query {SELECT s,change FROM actsits}
    } -cleanup {
        cleanup
    } -result {
s change 
- ------ 
1        
    }

    test analyze-1.3 {Change in coverage updates} -setup {
        setup
        deploy NB1 BLUE 15
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes

        # Deploy 15 more
        redeploy NB1 BLUE 30

        # Run for 7 days; deployment changes don't take effect until next tock.
        ted order SIM:RUN days 7 block yes

        ted query {SELECT * FROM actsits}
    } -cleanup {
        cleanup
    } -result {
s kind     stype    driver n   coverage           state  ts tc change  flist g    signature         a        
- -------- -------- ------ --- ------------------ ------ -- -- ------- ----- ---- ----------------- -------- 
1 ::actsit PRESENCE 1      NB1 0.7324194794132565 ACTIVE 0  7  UPDATED ALL   BLUE PRESENCE-1-1 0.73 PRESENCE 
    }


    test analyze-1.4 {Nominal personnel = 0 terminates} -setup {
        setup
        deploy NB1 BLUE 15
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes
        undeploy NB1 BLUE
        ted order SIM:RUN days 7 block yes

        ted query {SELECT * FROM actsits}
    } -cleanup {
        cleanup
    } -result {
s kind     stype    driver n   coverage state ts tc change flist g    signature         a        
- -------- -------- ------ --- -------- ----- -- -- ------ ----- ---- ----------------- -------- 
1 ::actsit PRESENCE 1      NB1 0.0      ENDED 0  7  ENDED  ALL   BLUE PRESENCE-2-1 none PRESENCE 
    }


    test analyze-1.5 {Can have multiple} -setup {
        setup
        deploy NB1 BLUE 30
        ted order ACTIVITY:SCHEDULE n NB1 g BLUE a PATROL personnel 15 start +0 pattern daily
        ted order ACTIVITY:SCHEDULE n NB1 g BLUE a GUARD  personnel 15 start +0 pattern daily
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes
        set out [ted query {SELECT * FROM actsits}]

        ted order SIM:RUN days 1 block yes
        append out [ted query {SELECT * FROM actsits}]

        undeploy NB1 BLUE
        ted order SIM:RUN days 7 block yes
        append out [ted query {SELECT * FROM actsits}]

        set out
    } -cleanup {
        cleanup
    } -result {
s kind     stype    driver n   coverage           state  ts tc change flist g    signature         a        
- -------- -------- ------ --- ------------------ ------ -- -- ------ ----- ---- ----------------- -------- 
1 ::actsit GUARD    1      NB1 0.4827181420282134 ACTIVE 0  0  NEW    ALL   BLUE GUARD-1-1 0.48    GUARD    
2 ::actsit PATROL   2      NB1 0.4827181420282134 ACTIVE 0  0  NEW    ALL   BLUE PATROL-1-1 0.48   PATROL   
3 ::actsit PRESENCE 3      NB1 0.7324194794132565 ACTIVE 0  0  NEW    ALL   BLUE PRESENCE-1-1 0.73 PRESENCE 
    
s kind     stype    driver n   coverage           state  ts tc change flist g    signature         a        
- -------- -------- ------ --- ------------------ ------ -- -- ------ ----- ---- ----------------- -------- 
1 ::actsit GUARD    1      NB1 0.4827181420282134 ACTIVE 0  0         ALL   BLUE GUARD-1-1 0.48    GUARD    
2 ::actsit PATROL   2      NB1 0.4827181420282134 ACTIVE 0  0         ALL   BLUE PATROL-1-1 0.48   PATROL   
3 ::actsit PRESENCE 3      NB1 0.7324194794132565 ACTIVE 0  0         ALL   BLUE PRESENCE-1-1 0.73 PRESENCE 
    
s kind     stype    driver n   coverage state ts tc change flist g    signature         a        
- -------- -------- ------ --- -------- ----- -- -- ------ ----- ---- ----------------- -------- 
1 ::actsit GUARD    1      NB1 0.0      ENDED 0  7  ENDED  ALL   BLUE GUARD-2-1 none    GUARD    
2 ::actsit PATROL   2      NB1 0.0      ENDED 0  7  ENDED  ALL   BLUE PATROL-2-1 none   PATROL   
3 ::actsit PRESENCE 3      NB1 0.0      ENDED 0  7  ENDED  ALL   BLUE PRESENCE-2-1 none PRESENCE 
    }

    #-------------------------------------------------------------------
    # get

    test get-1.1 {get -all} -setup {
        setup
        deploy NB1 BLUE 30
    } -body {
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes
        
        set sit [actsit get 1]
        $sit oneliner
    } -cleanup {
        cleanup
    } -result {BLUE PRESENCE in NB1}


    test get-1.2 {get -live} -setup {
        setup
        deploy NB1 BLUE 30
        ted order SIM:LOCK
        ted order SIM:RUN days 1 block yes
        undeploy NB1 BLUE
        ted order SIM:RUN days 7 block yes
    } -body {
        set sit [actsit get 1 -live]
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {no such live situation: "1"}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







