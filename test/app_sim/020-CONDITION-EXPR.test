# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-CONDITION-EXPR.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) CONDITION:EXPR:* order tests.
#
#    This test suite tests the CONDITION:EXPR:* orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted create JOE
        ted order TACTIC:DEPOSIT:CREATE owner JOE amount 10
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard items

    set C1 {
        cc_id  1
        text1  {1 < 2}
    }

    #-------------------------------------------------------------------
    # CONDITION:EXPR:CREATE

    test CREATE-1.1 {required parms} -body {
        ted order -reject CONDITION:EXPR:CREATE {}
    } -result {
        cc_id {required value}
        text1 {required value}
    }

    
    test CREATE-1.2 {tactic is unknown} -setup {
        setup
    } -body {
        ted order -reject CONDITION:EXPR:CREATE \
            [dict merge $C1 [list cc_id NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        cc_id {Invalid goal or tactic ID: "NONESUCH"}
    }


    test CREATE-2.1 {condition is created} -setup {
        setup
    } -body {
        ted order CONDITION:EXPR:CREATE $C1

        ted querylist {SELECT * FROM conditions_EXPR}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  EXPR
cc_id           1
narrative       Expression: 1 < 2
state           normal
flag            
text1           1 < 2
    }


    test CREATE-2.2 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <conditions>
        ted order CONDITION:EXPR:CREATE $C1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    test CREATE-3.1 {undo effects} -setup {
        setup
        ted order CONDITION:EXPR:CREATE $C1
    } -body {
        ted notifier bind ::rdb <conditions>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> delete 1}
    }


    #-------------------------------------------------------------------
    # CONDITION:EXPR:UPDATE

    proc setup_update {} {
        variable C1
        setup
        ted order CONDITION:EXPR:CREATE $C1
    }

    test UPDATE-1.1 {required parms} -body {
        ted order -reject CONDITION:EXPR:UPDATE {}
    } -result {
        condition_id {required value}
    }

    
    test UPDATE-1.2 {condition_id is unknown} -setup {
        setup_update
    } -body {
        ted order -reject CONDITION:EXPR:UPDATE condition_id NONESUCH text1 10
    } -cleanup {
        cleanup
    } -result {
        condition_id {Invalid condition ID: "NONESUCH"}
    }

    test UPDATE-1.3 {wrong flavor of condition} -setup {
        setup_update
        rdb eval {UPDATE conditions SET condition_type='BOGUS'}
    } -body {
        ted order -reject CONDITION:EXPR:UPDATE condition_id 1 text1 5
    } -cleanup {
        cleanup
    } -result {
        condition_id {Condition 1 is not a EXPR condition}
    }

    test UPDATE-2.1 {activities are updated} -setup {
        setup_update
    } -body {
        ted order CONDITION:EXPR:UPDATE condition_id 1 text1 {3 < 4}

        ted querylist {SELECT * FROM conditions_EXPR}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  EXPR
cc_id           1
narrative       Expression: 3 < 4
state           normal
flag            
text1           3 < 4
    }


    test UPDATE-2.2 {Effects} -setup {
        setup_update
    } -body {
        ted notifier bind ::rdb <conditions>
        ted order CONDITION:EXPR:UPDATE condition_id 1 text1 {3 < 4}
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    test UPDATE-3.1 {undo effects} -setup {
        setup_update
        ted order CONDITION:EXPR:UPDATE condition_id 1 text1 {3 < 4}
    } -body {
        ted notifier bind ::rdb <conditions>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







