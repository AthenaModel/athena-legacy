# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-condition.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    condition(sim) tests.
#
#    This test suite tests the condition(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        variable gid
        variable tid

        ted create JOE BOB NB1 NB2 BLUE BRIT
        ted order GOAL:CREATE owner BOB narrative "Sample Goal"
        ted order TACTIC:DEPOSIT:CREATE owner JOE amount 10 

        set gid [lindex [goal names]   0]
        set tid [lindex [tactic names] 0]

        # Verify that unused columns contain NULL
        rdb nullvalue "@"
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        rdb nullvalue ""
        array unset parms
        ted cleanup
    }

    proc mkupdate {condition_id parms} {
        set dict {
            a     ""
            text1 ""
            x1    ""
        }

        dict set dict condition_id $condition_id
        return [dict merge $dict $parms]
    }

    # Standard Conditions.
    # These are not real conditions; they are used for exercising
    # the mutators.
    
    set S1 {
        cc_id          1
        condition_type CASH
        a              JOE
        op1            GT
        x1             10000
        list1          {A B C}
        text1          FOO
    }

    set S2 {
        cc_id          1
        condition_type CASH
        a              BOB
        op1            LT
        x1             20000
        list1          {D E F}
        text1          BAR
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {condition is created} -setup {
        setup
    } -body {
        condition mutate create $S1
        ted querylist {SELECT * FROM conditions}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  CASH
cc_id           1
owner           BOB
narrative       Actor JOE's cash reserve is greater than $10,000
state           normal
flag            @
a               JOE
g               @
n               @
op1             GT
t1              @
t2              @
text1           FOO
list1           A B C
int1            @
x1              10000.0
    }

    test create-2.1 {undo deletes the condition} -setup {
        setup
        set undo [condition mutate create $S1]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT * FROM conditions}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Condition is deleted} -setup {
        setup
        condition mutate create $S1
        condition mutate create $S2
    } -body {
        condition mutate delete 1

        rdb eval {SELECT condition_id FROM conditions}
    } -cleanup {
        cleanup
    } -result {2}

    test delete-2.1 {Undo restores condition} -setup {
        setup
        condition mutate create $S1
        condition mutate create $S2
        set undo [condition mutate delete 2]
    } -body {
        namespace eval :: $undo
        ted querylist {SELECT * FROM conditions}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  CASH
cc_id           1
owner           BOB
narrative       Actor JOE's cash reserve is greater than $10,000
state           normal
flag            @
a               JOE
g               @
n               @
op1             GT
t1              @
t2              @
text1           FOO
list1           A B C
int1            @
x1              10000.0

condition_id    2
condition_type  CASH
cc_id           1
owner           BOB
narrative       Actor BOB's cash reserve is less than $20,000
state           normal
flag            @
a               BOB
g               @
n               @
op1             LT
t1              @
t2              @
text1           BAR
list1           D E F
int1            @
x1              20000.0
    }

    #-------------------------------------------------------------------
    # mutate update

    test update-1.1 {condition is updated} -setup {
        setup
        condition mutate create $S2
    } -body {
        condition mutate update [mkupdate 1 {
            x1  5000
        }]

        # This verifies both that requested changes are made, and
        # that NULL columns remain NULL.
        ted querylist {SELECT * FROM conditions}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  CASH
cc_id           1
owner           BOB
narrative       Actor BOB's cash reserve is less than $5,000.00
state           normal
flag            @
a               BOB
g               @
n               @
op1             LT
t1              @
t2              @
text1           BAR
list1           D E F
int1            @
x1              5000.0
    }

    test update-1.2 {empty values are OK} -setup {
        setup
        condition mutate create $S2
    } -body {
        set a [ted query {SELECT * FROM conditions}]

        condition mutate update [mkupdate 1 {}]

        set b [ted query {SELECT * FROM conditions}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        setup
        condition mutate create $S2
    } -body {

        set a [ted query {SELECT * FROM conditions}]

        set undo [condition mutate update [mkupdate 1 {
            text1        EQ
            x1           1000
        }]]

        namespace eval :: $undo

        set b [ted query {SELECT * FROM conditions}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # mutate state

    test state-1.1 {condition state is changed} -setup {
        setup
        condition mutate create $S2
    } -body {
        condition mutate state 1 disabled

        # This verifies both that requested changes are made, and
        # that NULL columns remain NULL.
        ted query {SELECT condition_id,state FROM conditions}
    } -cleanup {
        cleanup
    } -result {
condition_id state    
------------ -------- 
1            disabled 
    }


    test state-2.1 {undo undoes the change} -setup {
        setup
        condition mutate create $S2
    } -body {

        set a [ted query {SELECT * FROM conditions}]

        set undo [condition mutate state 1 disabled]

        namespace eval :: $undo

        set b [ted query {SELECT * FROM conditions}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns names: none defined} -body {
        condition names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns names} -setup {
        setup
        condition mutate create $S1
        condition mutate create $S2
    } -body {
        condition names
    } -cleanup {
        cleanup
    } -result {1 2}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid ID: none defined} -body {
        condition validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid condition ID: "NONESUCH"}

    test validate-2.1 {Valid name} -setup {
        setup
        condition mutate create $S1
    } -body {
        condition validate 1
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # get

    proc setup_get {} {
        variable S2
        setup
        condition mutate create $S2
    }

    test get-1.1 {get condition dict} -setup {
        setup_get
    } -body {
        ted pdict [condition get 1]
    } -cleanup {
        cleanup
    } -result {
        condition_type CASH
        op1            LT
        state          normal
        a              BOB
        cc_id          1
        flag           @
        condition_id   1
        int1           @
        narrative      {Actor BOB's cash reserve is less than $20,000}
        list1          {D E F}
        t1             @
        t2             @
        g              @
        x1             20000.0
        owner          BOB
        text1          BAR
        n              @
    }

    test get-1.2 {get parameter} -setup {
        setup_get
    } -body {
        condition get 1 op1
    } -cleanup {
        cleanup
    } -result {LT}

    #-------------------------------------------------------------------
    # compare

    test compare-1.1 {EQ} -body {
        list \
            [condition compare 1 EQ 1] \
            [condition compare 1 EQ 2]
    } -cleanup {
        cleanup
    } -result {1 0}

    test compare-1.2 {GE} -body {
        list \
            [condition compare 1 GE 1] \
            [condition compare 2 GE 1] \
            [condition compare 1 GE 2] \
    } -cleanup {
        cleanup
    } -result {1 1 0}

    test compare-1.3 {GT} -body {
        list \
            [condition compare 1 GT 1] \
            [condition compare 2 GT 1] \
            [condition compare 1 GT 2] \
    } -cleanup {
        cleanup
    } -result {0 1 0}

    test compare-1.4 {LE} -body {
        list \
            [condition compare 1 LE 1] \
            [condition compare 2 LE 1] \
            [condition compare 1 LE 2] \
    } -cleanup {
        cleanup
    } -result {1 0 1}

    test compare-1.5 {LT} -body {
        list \
            [condition compare 1 LT 1] \
            [condition compare 2 LT 1] \
            [condition compare 1 LT 2] \
    } -cleanup {
        cleanup
    } -result {0 0 1}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







