# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-coop.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    coop(sim) tests.
#
#    This test suite tests the coop(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate reconcile

    # Create cooperations


    test reconcile-1.1 {civ groups with force groups} -setup {
        ted create NB1 NB2 BLUE USAID SHIA SUNN KURD
    } -body {
        coop mutate reconcile
        
        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    coop0 atrend athresh dtrend dthresh 
---- ---- ----- ------ ------- ------ ------- 
SHIA BLUE 50.0  0.0    50.0    0.0    50.0    
SUNN BLUE 50.0  0.0    50.0    0.0    50.0    
KURD BLUE 50.0  0.0    50.0    0.0    50.0    
    }

    test reconcile-1.3 {Created curves are undone} -setup {
        ted create NB1 BLUE SHIA
        set undo [coop mutate reconcile]
    } -body {
        eval $undo
        rdb query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {}
    

    test reconcile-2.1 {cooperations deleted with frc group} -setup {
        ted create NB1 BLUE BRIT SHIA
        coop mutate reconcile
    } -body {
        frcgroup mutate delete BLUE
        coop mutate reconcile

        # SHIA with BLUE remains
        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    coop0 atrend athresh dtrend dthresh 
---- ---- ----- ------ ------- ------ ------- 
SHIA BRIT 50.0  0.0    50.0    0.0    50.0    
    }


    test reconcile-2.2 {cooperations deleted with group} -setup {
        ted create NB1 BLUE SHIA SUNN
        coop mutate reconcile
    } -body {
        civgroup mutate delete SHIA
        coop mutate reconcile

        # SUNN remains
        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    coop0 atrend athresh dtrend dthresh 
---- ---- ----- ------ ------- ------ ------- 
SUNN BLUE 50.0  0.0    50.0    0.0    50.0    
    }


    test reconcile-2.4 {On undo: deleted cooperations are restored} -setup {
        # FIRST, create the groups and cooperations
        ted create NB1 BLUE SHIA
        coop mutate reconcile

        # NEXT, update one, so that we can see that it's restored
        coop mutate update [list id {SHIA BLUE} coop0 40.0]

        # NEXT, delete the groups and cooperations, and get the
        # undo script.
        civgroup mutate delete SHIA

        set undo [coop mutate reconcile]
    } -body {
        eval $undo
        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    coop0 atrend athresh dtrend dthresh 
---- ---- ----- ------ ------- ------ ------- 
SHIA BLUE 40.0  0.0    50.0    0.0    50.0    
    }

    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {coop is updated} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
    } -body {
        coop mutate update {
            id      {SHIA BLUE}
            coop0   40.0
            atrend  5.0
            athresh 40
            dtrend  -5.0
            dthresh 60
        }

        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    coop0 atrend athresh dtrend dthresh 
---- ---- ----- ------ ------- ------ ------- 
SHIA BLUE 40.0  5.0    40.0    -5.0   60.0    
    }


    test update-1.2 {identical values are OK} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
        set parmdict {
            id      {SHIA BLUE}
            coop0   40.0
            atrend  5.0
            athresh 40
            dtrend  -5.0
            dthresh 60
        }

        coop mutate update $parmdict
    } -body {
        set a [ted query {SELECT * FROM coop_fg}]
        
        coop mutate update $parmdict

        set b [ted query {SELECT * FROM coop_fg}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
    } -body {
        set a [ted query {SELECT * FROM coop_fg}]

        coop mutate update  {
            id      {SHIA BLUE}
            coop0   ""
            atrend  ""
            athresh ""
            dtrend  ""
            dthresh ""
        }

        set b [ted query {SELECT * FROM coop_fg}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
    } -body {

        set a [ted query {SELECT * FROM coop_fg}]

        set undo [coop mutate update {
            id      {SHIA BLUE}
            coop0   40.0
            atrend  5.0
            athresh 40
            dtrend  -5.0
            dthresh 60
        }]

        eval $undo

        set b [ted query {SELECT * FROM coop_fg}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # exists

    test exists-1.1 {coop does not exist} -body {
        coop exists NONESUCH NONESUCH
    } -cleanup {
        cleanup
    } -result {0}


    test exists-1.2 {coop exists} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
    } -body {
        coop exists SHIA BLUE
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid group f} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid civilian group, should be one of: SHIA}


    test validate-1.2 {Invalid group g} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop validate {SHIA NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid force group, should be one of: BLUE}


    test validate-2.1 {Valid group} -setup {
        ted create NB1 BLUE SHIA
        coop mutate reconcile
    } -body {
        coop validate {SHIA BLUE}
    } -cleanup {
        cleanup
    } -result {SHIA BLUE}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





