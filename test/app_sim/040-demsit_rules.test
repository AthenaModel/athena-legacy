# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#   040-demsit_rules.test
#
# AUTHOR:
#   Will Duquette
#
# DESCRIPTION:
#   demsit_rules(sim) tests
#
#   This test suite tests that the rules fire when appropriate.
#
# NOTE:
#   This test suite differs from the other 040-* test suites.
#
#   The 040-* tests have the following purposes, in order of importance:
#
#   1. Elimination of lurking bgerrors.
#   2. Verification that the correct rules fire, given the rule set inputs
#   3. Verification that the rules provide the correct kinds of inputs to
#      URAM.
#   4. Verification that the other Athena models will give correct inputs
#      to the rule sets.
#
#   In short, the 040-* test suites test not only the rule sets themselves,
#   but (as noted in point #4) the models that drive the rule sets.
#
#   However, the both the CGE and the cellmodel used to initialize it are
#   in a state of flux; if we use the CGE outputs to drive this test suite,
#   we will be having to modify the tests on a weekly basis as the economic
#   model changes.
#
#   Thus, in this test suite we will skip point #4, and focus narrowly
#   on just the rule sets themselves.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # setup 
    #
    # Sets up for tests.
    
    proc setup {} {
        # FIRST, disable the economic model.  (See above.)
        parm set econ.disable yes
        
        # FIRST, other rule sets shouldn't muddy the waters.
        parm set dam.PRESENCE.active no
        parm set dam.ENI.active no
        rdb eval {DELETE FROM drivers}
        rdb eval {DELETE FROM mads_t}
        
        # NEXT, create the entities
        ted create JOE BOB DAVE NB1 NB2 TAL SHIA SUNN KURD
        
        # NEXT, lock, so that URAM is ready to receive inputs.
        ted lock
    }
    
    # consump emult povfrac ?controller?
    #
    # emult      - ELOC multiplier; ELOC = emult * ALOC
    # povfrac    - The poverty fraction
    # controller - The controller of neighborhood NB1
    #
    # Sets up the demog_g table, and assesses CONSUMP for group SHIA.
    
    proc consump {emult povfrac {controller ""}} {
        set aloc 1000.0
        let eloc {$emult * $aloc}
    
        rdb eval {
            UPDATE control_n
            SET controller = nullif($controller, '')
            WHERE n = 'NB1';
            
            UPDATE demog_g
            SET aloc = $aloc, eloc = $aloc, povfrac = 0;
            
            UPDATE demog_g
            SET aloc    = $aloc,
                eloc    = $eloc,
                povfrac = $povfrac
            WHERE g = 'SHIA'
        }
    
        demsit_rules monitor CONSUMP
    }
    
    # unemp uaf
    #
    # uaf  - The unemployment attitude factor for NB1
    #
    # Sets up the demog_n tables with the specified
    # factors, all other factors being 0.0, and calls demsit_rules
    # to assess the results.
    
    proc unemp {uaf} {
        # FIRST, spoof the factors.
        rdb eval {
            UPDATE demog_n SET uaf = 0.0;
            UPDATE demog_n SET uaf = $uaf
            WHERE n = 'NB1';
        }
        
        # NEXT, execute the rule set.
        demsit_rules monitor UNEMP
    }
    
    # Clean up after a test
    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # CONSUMP
    
    test CONSUMP-1.1 {Rule 1-1 fires, effectf not zero} -setup {
        setup
    } -body {
        consump 2.0 0.0
        ted querylist {
            SELECT * FROM rule_firings where rule GLOB 'CONSUMP-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  1
t          0
driver_id  1000
ruleset    CONSUMP
rule       CONSUMP-1-1
fdict      a {} povfrac 0.0 n NB1 eloc 2000.0 aloc 1000.0 g SHIA expectf -1.5 povf 0.00
    }
    
    test CONSUMP-1.2 {Rule 1-1 inputs, expectf not zero} -setup {
        setup
    } -body {
        consump 2.0 0.0 
        ted query {
            SELECT rule, g, c
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CONSUMP-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule        g    c   
----------- ---- --- 
CONSUMP-1-1 SHIA AUT 
CONSUMP-1-1 SHIA QOL 
    }
    
    test CONSUMP-1.3 {Rule 1-1 fires, povf not zero} -setup {
        setup
    } -body {
        consump 1.0 1.0
        ted querylist {
            SELECT * FROM rule_firings where rule GLOB 'CONSUMP-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  1
t          0
driver_id  1000
ruleset    CONSUMP
rule       CONSUMP-1-1
fdict      a {} povfrac 1.0 n NB1 eloc 1000.0 aloc 1000.0 g SHIA expectf 0.0 povf 1.00
    }
    
    test CONSUMP-1.4 {Rule 1-1 inputs, povf not zero} -setup {
        setup
    } -body {
        consump 1.0 1.0 
        ted query {
            SELECT rule, g, c
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CONSUMP-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule        g    c   
----------- ---- --- 
CONSUMP-1-1 SHIA AUT 
CONSUMP-1-1 SHIA QOL 
    }
    
    test CONSUMP-2.1 {Rule 2-1 fires} -setup {
        setup
    } -body {
        consump 1.0 1.0 JOE
        
        ted querylist {
            SELECT * FROM rule_firings where rule GLOB 'CONSUMP-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  2
t          0
driver_id  1000
ruleset    CONSUMP
rule       CONSUMP-2-1
fdict      a JOE povfrac 1.0 n NB1 eloc 1000.0 aloc 1000.0 g SHIA expectf 0.0 povf 1.00
    }
    
    test CONSUMP-2.2 {Rule 2-1 doesn't fire with no controller} -setup {
        setup
    } -body {
        consump 1.0 1.0
        
        rdb eval {
            SELECT rule FROM rule_firings
        }
    } -cleanup {
        cleanup
    } -result {CONSUMP-1-1}
    
    test CONSUMP-2.3 {Rule 2-1 inputs} -setup {
        setup
    } -body {
        consump 1.0 1.0 JOE
        ted query {
            SELECT rule, g, a
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CONSUMP-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule        g    a   
----------- ---- --- 
CONSUMP-2-1 SHIA JOE 
    }
    
    test CONSUMP-3.1 {Rule 2-2 fires} -setup {
        setup
    } -body {
        consump 2.0 1.0 JOE
        
        ted querylist {
            SELECT * FROM rule_firings where rule GLOB 'CONSUMP-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  2
t          0
driver_id  1000
ruleset    CONSUMP
rule       CONSUMP-2-2
fdict      a JOE povfrac 1.0 n NB1 eloc 2000.0 aloc 1000.0 g SHIA expectf -1.5 povf 1.00
    }
    
    test CONSUMP-3.2 {Rule 2-2 doesn't fire with no controller} -setup {
        setup
    } -body {
        consump 2.0 1.0
        
        rdb eval {
            SELECT rule FROM rule_firings
        }
    } -cleanup {
        cleanup
    } -result {CONSUMP-1-1}
    
    test CONSUMP-3.3 {Rule 2-2 inputs} -setup {
        setup
    } -body {
        consump 2.0 1.0 JOE
        ted query {
            SELECT rule, g, a
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CONSUMP-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule        g    a   
----------- ---- --- 
CONSUMP-2-2 SHIA JOE 
    }

    
    #-------------------------------------------------------------------
    # UNEMP
    
    test UNEMP-1.1 {Rule 1-1 fires} -setup {
        setup
    } -body {
        unemp 1.0
        ted querylist {
            SELECT * FROM rule_firings where ruleset='UNEMP'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  1
t          0
driver_id  1000
ruleset    UNEMP
rule       UNEMP-1-1
fdict      uaf 1.0 n NB1 g SHIA

firing_id  2
t          0
driver_id  1001
ruleset    UNEMP
rule       UNEMP-1-1
fdict      uaf 1.0 n NB1 g SUNN
    }
    
    test UNEMP-1.2 {Rule 1-1 inputs} -setup {
        setup
    } -body {
        unemp 1.0
        ted query {
            SELECT rule, g, c
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE ruleset='UNEMP'
        }
    } -cleanup {
        cleanup
    } -result {
rule      g    c   
--------- ---- --- 
UNEMP-1-1 SHIA SFT 
UNEMP-1-1 SHIA AUT 
UNEMP-1-1 SUNN SFT 
UNEMP-1-1 SUNN AUT 
    }
    
    cleanupTests
}

namespace delete ::athena_test::tests::







