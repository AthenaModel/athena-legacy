# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-rel.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    rel(sim) tests.
#
#    This test suite tests the rel(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {override is created} -setup {
        ted create BLUE SHIA
    } -body {
        rel mutate create {id {SHIA BLUE} rel 0.5}
        ted query {SELECT * FROM rel_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    rel 
---- ---- --- 
SHIA BLUE 0.5 
    }

    test create-2.1 {undo deletes the override} -setup {
        ted create BLUE SHIA
        set undo [rel mutate create {id {SHIA BLUE} rel 0.5}]
    } -body {
        namespace eval :: $undo
        rdb eval {SELECT * FROM rel_fg}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Record is deleted} -setup {
        ted create BLUE SHIA
        rel mutate create {id {SHIA BLUE} rel 0.5}
    } -body {
        rel mutate delete {SHIA BLUE}

        # Is SHIA gone?
        rdb eval {SELECT f,g FROM rel_fg}
    } -cleanup {
        cleanup
    } -result {}

    test delete-2.1 {Undo restores record} -setup {
        ted create BLUE SHIA
        rel mutate create {id {SHIA BLUE} rel 0.5}
        set undo [rel mutate delete {SHIA BLUE}]
    } -body {
        namespace eval :: $undo
        ted query {SELECT * FROM rel_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    rel 
---- ---- --- 
SHIA BLUE 0.5 
    }


    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {rel is updated} -setup {
        ted create BLUE BRIT
        rel mutate create {id {BLUE BRIT} rel 0.5}
    } -body {
        rel mutate update {
            id   {BLUE BRIT}
            rel  0.7
        }

        ted query {SELECT * FROM rel_fg ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {
f    g    rel 
---- ---- --- 
BLUE BRIT 0.7 
    }


    test update-1.2 {identical values are OK} -setup {
        ted create BLUE BRIT

        set parmdict {
            id   {BLUE BRIT}
            rel  0.5
        }

        rel mutate create $parmdict
    } -body {
        set a [ted query {SELECT * FROM rel_fg ORDER BY f,g}]
        
        rel mutate update $parmdict

        set b [ted query {SELECT * FROM rel_fg ORDER BY f,g}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create BLUE BRIT
        rel mutate create {id {BLUE BRIT} rel 0.5}
    } -body {
        set a [ted query {SELECT * FROM rel_fg ORDER BY f,g}]

        rel mutate update  {
            id   {BLUE BRIT}
            rel  ""
        }

        set b [ted query {SELECT * FROM rel_fg ORDER BY f,g}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create BLUE BRIT
        rel mutate create {id {BLUE BRIT} rel 0.0}
    } -body {

        set a [ted query {SELECT * FROM rel_fg ORDER BY f,g}]

        set undo [rel mutate update {
            id   {BLUE BRIT}
            rel  0.5
        }]

        eval $undo

        set b [ted query {SELECT * FROM rel_fg ORDER BY f,g}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # exists

    test exists-1.1 {groups do exist} -body {
        rel exists {NONESUCH NONESUCH}
    } -cleanup {
        cleanup
    } -result {0}


    test exists-1.2 {record does not exist} -body {
        ted create BLUE BRIT
        rel exists {BLUE BRIT}
    } -cleanup {
        cleanup
    } -result {0}

    test exists-1.3 {record exists} -setup {
        ted create BLUE BRIT
        rel mutate create {id {BLUE BRIT} rel 0.5}
    } -body {
        rel exists {BLUE BRIT}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid group f} -body {
        rel validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -result {Invalid group, none are defined}


    test validate-1.2 {Invalid group g} -setup {
        ted create BLUE BRIT
    } -body {
        rel validate {BLUE NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid group, should be one of: BLUE, BRIT}


    test validate-1.3 {f eq g} -setup {
        ted create BLUE BRIT
    } -body {
        rel validate {BLUE BLUE}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {A group's relationship with itself cannot be overridden.}

    test validate-2.1 {Valid relationship} -setup {
        ted create BLUE BRIT
    } -body {
        rel validate {BLUE BRIT}
    } -cleanup {
        cleanup
    } -result {BLUE BRIT}

    #-------------------------------------------------------------------
    # rel_view
    #
    # The rel_view is implemented in scenariodb.sql, not in rel.tcl,
    # so perhaps it doesn't belong here; on the other hand, it pulls
    # together affinities from the bsystem with relationship overrides
    # from rel_fg, and it ought to be tested somewhere.

    test rel_view-1.1 {Defaults to 1.0 and 0.0} -setup {
        ted create BLUE BRIT
    } -body {
        ted query {SELECT * FROM rel_view ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {
f    g    rel override 
---- ---- --- -------- 
BLUE BLUE 1.0 0        
BLUE BRIT 0.0 0        
BRIT BLUE 0.0 0        
BRIT BRIT 1.0 0        
    }

    test rel_view-1.2 {rel_fg overrides bsystem values} -setup {
        ted create BLUE BRIT
    } -body {
        rel mutate create {id {BLUE BRIT} rel 0.5}
        ted query {SELECT * FROM rel_view ORDER BY f,g}
    } -cleanup {
        cleanup
    } -result {
f    g    rel override 
---- ---- --- -------- 
BLUE BLUE 1.0 0        
BLUE BRIT 0.5 1        
BRIT BLUE 0.0 0        
BRIT BRIT 1.0 0        
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





