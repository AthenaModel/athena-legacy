# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-defroe.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    defroe(sim) tests.
#
#    This test suite tests the defroe(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate reconcile

    test reconcile-1.1 {create defending ROEs} -setup {
        ted create NB1 NB2 BLUE ALQ
    } -body {
        defroe mutate reconcile

        ted query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    roe                  
--- ---- -------------------- 
NB1 BLUE FIRE_BACK_IF_PRESSED 
NB2 BLUE FIRE_BACK_IF_PRESSED 
    }


    test reconcile-1.3 {Created curves are undone} -setup {
        ted create NB1 NB2 BLUE
        set undo [defroe mutate reconcile]
    } -body {
        eval $undo
        rdb query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {}
    

    test reconcile-2.1 {Delete ROEs with nbhood} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
    } -body {
        nbhood mutate delete NB2
        defroe mutate reconcile

        ted query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    roe                  
--- ---- -------------------- 
NB1 BLUE FIRE_BACK_IF_PRESSED 
    }


    test reconcile-2.2 {Delete ROEs with group} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
    } -body {
        frcgroup mutate delete BLUE
        defroe mutate reconcile

        # Should be none.
        rdb query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {}

    test reconcile-2.4 {On undo: ROEs are restored} -setup {
        # FIRST, create the groups and ROEs
        ted create NB1 NB2 BLUE
        defroe mutate reconcile

        # NEXT, delete the group and ROEs and get the 
        # undo script.
        frcgroup mutate delete BLUE

        set undo [defroe mutate reconcile]
    } -body {
        eval $undo
        ted query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    roe                  
--- ---- -------------------- 
NB1 BLUE FIRE_BACK_IF_PRESSED 
NB2 BLUE FIRE_BACK_IF_PRESSED 
    }


    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {defroe is updated} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
    } -body {
        defroe mutate update {
            id      {NB1 BLUE}
            defroe  HOLD_FIRE
        }

        ted query {SELECT * FROM defroe_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    roe                  
--- ---- -------------------- 
NB1 BLUE FIRE_BACK_IF_PRESSED 
NB2 BLUE FIRE_BACK_IF_PRESSED 
    }


    test update-1.2 {identical values are OK} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
        set parmdict {
            id      {NB1 BLUE}
            defroe  HOLD_FIRE
        }

        defroe mutate update $parmdict
    } -body {
        set a [ted query {SELECT * FROM defroe_ng}]
        
        defroe mutate update $parmdict

        set b [ted query {SELECT * FROM defroe_ng}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
    } -body {
        set a [ted query {SELECT * FROM defroe_ng}]

        defroe mutate update  {
            id      {NB1 BLUE}
            defroe  HOLD_FIRE
        }

        set b [ted query {SELECT * FROM defroe_ng}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create NB1 NB2 BLUE
        defroe mutate reconcile
    } -body {

        set a [ted query {SELECT * FROM defroe_ng}]

        set undo [defroe mutate update {
            id      {NB1 BLUE}
            defroe  HOLD_FIRE
        }]

        eval $undo

        set b [ted query {SELECT * FROM defroe_ng}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid nbhood} -body {
        ted create NB1 NB2 BLUE BRIT
        defroe validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid neighborhood, should be one of: NB1, NB2}


    test validate-1.2 {invalid group g} -setup {
        ted create NB1
    } -body {
        defroe validate {NB1 NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid uniformed force group, none are defined}


    test validate-2.1 {Valid ROE} -setup {
        ted create NB1 BLUE
        defroe mutate reconcile
    } -body {
        defroe validate {NB1 BLUE}
    } -cleanup {
        cleanup
    } -result {NB1 BLUE}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





