# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_mobilize.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_mobilize(sim) tests.
#
#    This test suite tests the tactic::MOBILIZE ensemble.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BOB NB1 NB2 NB1_BLUE NB1_BRIT

        # Verify that unused columns contain NULL
        rdb nullvalue "@"
    }

    proc execute_setup {} {
        setup
        personnel start
        personnel load
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        rdb nullvalue ""
        array unset parms
        ted cleanup
    }

    set T1 {
        tactic_type MOBILIZE
        owner       JOE
        priority    top
        g           BLUE
        int1        100
        once        1
    }

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::MOBILIZE narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Mobilize 100 new BLUE personnel.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {check: g no longer exists} -setup {
        setup
        tactic mutate create $T1
        rdb eval {DELETE FROM frcgroups WHERE g='BLUE'}
    } -body {
        tactic::MOBILIZE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Force/organization group BLUE no longer exists.}

    test check-1.2 {check: g no longer owned by owner} -setup {
        setup
        tactic mutate create $T1
        rdb eval {UPDATE frcgroups SET a=NULL}
    } -body {
        tactic::MOBILIZE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Force/organization group BLUE is no longer owned by actor JOE.}

    #-------------------------------------------------------------------
    # dollars

    test dollars-1.1 {dollars} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::MOBILIZE dollars [tactic get 1]
    } -cleanup {
        cleanup
    } -result {0.00}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {execute: 1 on success} -setup {
        execute_setup
        tactic mutate create $T1
    } -body {
        tactic::MOBILIZE execute [tactic get 1]
    } -cleanup {
        cleanup
    } -result {1}

    test execute-1.2 {execute: effects} -setup {
        execute_setup
        tactic mutate create $T1
    } -body {
        tactic::MOBILIZE execute [tactic get 1]
        ted query {SELECT * FROM working_personnel}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5100      5100      
BRIT 5000      5000      
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





