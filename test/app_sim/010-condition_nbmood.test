# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-condition_nbmood.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    condition_nbmood(sim) tests.
#
#    This test suite tests the condition_nbmood(sim) ensemble.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BOB NB1 NB2 BLUE BRIT

        ted order TACTIC:SAVE:CREATE owner JOE int1 10 on_lock NO

        # Verify that unused columns contain NULL
        rdb nullvalue "@"
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        rdb nullvalue ""
        array unset parms
        ted cleanup
    }

    # Standard Conditions.
    
    set C1 {
        cc_id          1
        condition_type NBMOOD
        n              NB1
        op1            LT
        x1             50.0
    }

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative} -setup {
        setup
        condition mutate create $C1
    } -body {
        condition::NBMOOD narrative [condition get 1]
    } -cleanup {
        cleanup
    } -result {Neighborhood NB1's mood is less than 50.00.}

    #-------------------------------------------------------------------
    # check


    test check-1.1 {check: successful} -setup {
        setup
        condition mutate create $C1
    } -body {
        condition::NBMOOD check [condition get 1]
    } -cleanup {
        cleanup
    } -result {}

    test check-1.2 {check: nbhood deleted} -setup {
        setup
        condition mutate create $C1
        nbhood mutate delete NB1
    } -body {
        condition::NBMOOD check [condition get 1]
    } -cleanup {
        cleanup
    } -result {Neighborhood NB1 no longer exists.}

    #-------------------------------------------------------------------
    # eval


    proc NbmoodEval {nbmood comp amount} {
        rdb eval {
            INSERT OR REPLACE INTO gram_n(n,sat)
            VALUES('NB1', $nbmood);
        }
        condition mutate update [list condition_id 1 op1 $comp x1 $amount]
        return [condition::NBMOOD eval [condition get 1]]
    }

    test eval-1.1 {Test eval} -setup {
        setup
        condition mutate create $C1
    } -body {
        set a [NbmoodEval 50 EQ 50]
        set b [NbmoodEval 50 GE 50]
        set c [NbmoodEval 50 LE 50]
        set d [NbmoodEval 50 GT 45]
        set e [NbmoodEval 50 LT 45]

        list $a $b $c $d $e
    } -cleanup {
        cleanup
    } -result {1 1 1 1 0}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







