# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-DEPOSIT.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) TACTIC:* order tests.
#
#    This test suite tests the TACTIC:DEPOSIT:* orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted create JOE
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard items

    set DEPOSIT1 {
        owner       JOE
        priority    bottom
        amount      1000.0
    }

    #-------------------------------------------------------------------
    # TACTIC:DEPOSIT:CREATE

    test DEPOSIT:CREATE-1.1 {required parms} -body {
        ted order -reject TACTIC:DEPOSIT:CREATE {}
    } -result {
        owner {required value}
        amount {required value}
    }

    
    test DEPOSIT:CREATE-1.2 {a is unknown} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEPOSIT:CREATE \
            [dict merge $DEPOSIT1 [list owner NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        owner {Invalid actor, should be one of: JOE}
    }

    test DEPOSIT:CREATE-1.3 {amount is invalid} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEPOSIT:CREATE \
            [dict merge $DEPOSIT1 [list amount NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        amount {invalid money value "NONESUCH", expected positive numeric value with optional K, M, or B suffix}
    }

    test DEPOSIT:CREATE-1.4 {priority is invalid} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEPOSIT:CREATE [dict merge $DEPOSIT1 {
            priority NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        priority {invalid value "nonesuch", should be one of: top, bottom}
    }

    test DEPOSIT:CREATE-2.1 {tactic is created} -setup {
        setup
    } -body {
        ted order TACTIC:DEPOSIT:CREATE $DEPOSIT1

        ted querylist {SELECT * FROM tactics_DEPOSIT}
    } -cleanup {
        cleanup
    } -result {
tactic_id    1
tactic_type  DEPOSIT
owner        JOE
narrative    Deposit $1,000.00 to the cash reserve.
priority     1
amount       1000.0
state        normal
exec_ts      
exec_flag    0
    }

    test DEPOSIT:CREATE-2.2 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <tactics>
        ted order TACTIC:DEPOSIT:CREATE $DEPOSIT1
        ted notifier received

        # NOTE: Two updates, because of priority setting.
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
        {::rdb <tactics> update 1}
    }


    test DEPOSIT:CREATE-3.1 {undo effects} -setup {
        setup
        ted order TACTIC:DEPOSIT:CREATE $DEPOSIT1
    } -body {
        ted notifier bind ::rdb <tactics>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> delete 1}
    }


    #-------------------------------------------------------------------
    # TACTIC:DEPOSIT:UPDATE

    proc setup_update_depositmoney {} {
        variable DEPOSIT1
        setup
        ted order TACTIC:DEPOSIT:CREATE $DEPOSIT1
    }

    test DEPOSIT:UPDATE-1.1 {required parms} -body {
        ted order -reject TACTIC:DEPOSIT:UPDATE {}
    } -result {
        tactic_id {required value}
        amount {required value}
    }

    
    test DEPOSIT:UPDATE-1.2 {tactic_id is unknown} -setup {
        setup_update_depositmoney
    } -body {
        ted order -reject TACTIC:DEPOSIT:UPDATE tactic_id NONESUCH amount 10
    } -cleanup {
        cleanup
    } -result {
        tactic_id {Invalid tactic ID: "NONESUCH"}
    }

    test DEPOSIT:UPDATE-1.3 {amount is invalid} -setup {
        setup_update_depositmoney
    } -body {
        ted order -reject TACTIC:DEPOSIT:UPDATE tactic_id 1 amount NONESUCH
    } -cleanup {
        cleanup
    } -result {
        amount {invalid money value "NONESUCH", expected positive numeric value with optional K, M, or B suffix}
    }

    test DEPOSIT:UPDATE-1.4 {wrong flavor of tactic} -setup {
        setup_update_depositmoney
        rdb eval {UPDATE tactics SET tactic_type='BOGUS'}
    } -body {
        ted order -reject TACTIC:DEPOSIT:UPDATE tactic_id 1 amount 5
    } -cleanup {
        cleanup
    } -result {
        tactic_id {Tactic 1 is not a DEPOSIT tactic}
    }

    test DEPOSIT:UPDATE-2.1 {activities are updated} -setup {
        setup_update_depositmoney
    } -body {
        ted order TACTIC:DEPOSIT:UPDATE tactic_id 1 amount 20

        ted querylist {SELECT * FROM tactics_DEPOSIT}
    } -cleanup {
        cleanup
    } -result {
tactic_id    1
tactic_type  DEPOSIT
owner        JOE
narrative    Deposit $20.00 to the cash reserve.
priority     1
amount       20.0
state        normal
exec_ts      
exec_flag    0
    }


    test DEPOSIT:UPDATE-2.2 {Effects} -setup {
        setup_update_depositmoney
    } -body {
        ted notifier bind ::rdb <tactics>
        ted order TACTIC:DEPOSIT:UPDATE tactic_id 1 amount 20
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
    }


    test DEPOSIT:UPDATE-3.1 {undo effects} -setup {
        setup_update_depositmoney
        ted order TACTIC:DEPOSIT:UPDATE tactic_id 1 amount 20
    } -body {
        ted notifier bind ::rdb <tactics>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <tactics> update 1}
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






