# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-UNIT.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) UNIT:* order tests.
#
#    This test suite tests the unit-related orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Standard units
    set u1 {
        cid       991
        n         NB1 
        g         BLUE  
        tn        NB1    
        a         PATROL    
        personnel 1
    }

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }



    #-------------------------------------------------------------------
    # UNIT:MOVE

    test MOVE-1.1 {required parms} -body {
        ted order -reject UNIT:MOVE {}
    } -result {
        u {required value}
        location {required value}
    }
    
    test MOVE-1.2 {invalid unit} -body {
        ted order -reject UNIT:MOVE u U1 location A00A00
    } -cleanup {
        cleanup
    } -result {
        u {Invalid unit name: "U1"}
    }


    test MOVE-1.3 {invalid location} -setup {
        ted create NB1 BLUE
        unit create $u1
    } -body {
        ted order -reject UNIT:MOVE u BLUE-NB1/0991 location NONESUCH
    } -cleanup {
        cleanup
    } -result {
        location {not a map reference string}
    }


    test MOVE-1.4 {location not in neighborhood} -setup {
        ted create NB1 BLUE
        unit create $u1
    } -body {
        ted order -reject UNIT:MOVE u BLUE-NB1/0991 location A00A00
    } -cleanup {
        cleanup
    } -result {
        location {Cannot remove unit from its neighborhood}
    }


    test MOVE-2.1 {unit is moved} -setup {
        ted create NB1 BLUE
        unit create $u1
    } -body {
        ted order UNIT:MOVE u BLUE-NB1/0991 location B01B01

        ted query {SELECT u,location FROM units}
    } -cleanup {
        cleanup
    } -result {
u             location 
------------- -------- 
BLUE-NB1/0991 101 101  
    }


    test MOVE-2.2 {<Entity> update} -setup {
        ted create NB1 BLUE
        unit create $u1
    } -body {
        ted notifier bind ::rdb <units>
        ted order UNIT:MOVE u BLUE-NB1/0991 location B01B01
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <units> update BLUE-NB1/0991}
    }


    test MOVE-3.1 {undo undoes the change} -setup {
        ted create NB1 BLUE
        unit create $u1
        ted order UNIT:MOVE u BLUE-NB1/0991 location B01B01
    } -body {
        ted notifier bind ::rdb <units>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <units> update BLUE-NB1/0991}
    }



    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





