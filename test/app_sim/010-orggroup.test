# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-orggroup.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    orggroup(sim) tests.
#
#    This test suite tests the orggroup(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {orggroup is created} -body {
        orggroup mutate create [ted entity USAID]
        ted query {SELECT * FROM orggroups_view}
    } -cleanup {
        cleanup
    } -result {
g     longname color   shape   symbol       gtype orgtype rollup_weight effects_factor 
----- -------- ------- ------- ------------ ----- ------- ------------- -------------- 
USAID US Aid   #000001 NEUTRAL organization ORG   NGO     1.0           1.1            
    }


    test create-1.2 {<Entity> create} -body {
        ted notifier bind ::orggroup <Entity>
        orggroup mutate create [ted entity USAID]
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> create USAID}
    }


    test create-2.1 {undo deletes the orggroup} -setup {
        orggroup mutate create [ted entity USAID]
        set undo [orggroup mutate create [ted entity HAL]]
    } -body {
        {*}$undo
        rdb eval {SELECT g FROM orggroups_view}
    } -cleanup {
        cleanup
    } -result {USAID}


    test create-2.2 {On undo, <Entity> delete} -setup {
        set undo [orggroup mutate create [ted entity USAID]]
    } -body {
        ted notifier bind ::orggroup <Entity>
        {*}$undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> delete USAID}
    }

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Group is deleted} -setup {
        ted create USAID HAL
    } -body {
        orggroup mutate delete USAID

        # Is USAID gone?
        rdb eval {SELECT g FROM orggroups_view}
    } -cleanup {
        cleanup
    } -result {HAL}


    test delete-1.2 {<Entity> delete} -setup {
        ted create USAID
    } -body {
        ted notifier bind ::orggroup <Entity>
        orggroup mutate delete USAID
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> delete USAID}
    }


    test delete-2.1 {Undo restores group} -setup {
        ted create USAID
        set undo [orggroup mutate delete USAID]
    } -body {
        {*}$undo
        ted query {SELECT * FROM orggroups_view}
    } -cleanup {
        cleanup
    } -result {
g     longname color   shape   symbol       gtype orgtype rollup_weight effects_factor 
----- -------- ------- ------- ------------ ----- ------- ------------- -------------- 
USAID US Aid   #000001 NEUTRAL organization ORG   NGO     1.0           1.1            
    }


    test delete-2.2 {On undo, <Entity> create} -setup {
        ted create USAID
        set undo [orggroup mutate delete USAID]
    } -body {
        ted notifier bind ::orggroup <Entity>
        {*}$undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> create USAID}
    }

    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {orggroup is updated} -setup {
        ted create USAID
    } -body {
        orggroup mutate update {
            g              USAID
            longname       "Red Cross"
            color          "#ff0000"
            shape          FRIEND
            orgtype        IGO
            rollup_weight  0.1
            effects_factor 0.2
        }

        ted query {SELECT * FROM orggroups_view}
    } -cleanup {
        cleanup
    } -result {
g     longname  color   shape  symbol       gtype orgtype rollup_weight effects_factor 
----- --------- ------- ------ ------------ ----- ------- ------------- -------------- 
USAID Red Cross #ff0000 FRIEND organization ORG   IGO     0.1           0.2            
    }


    test update-1.2 {identical values are OK} -setup {
        ted create USAID
    } -body {
        set a [ted query {SELECT * FROM orggroups_view}]

        orggroup mutate update [ted entity USAID]

        set b [ted query {SELECT * FROM orggroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create USAID
    } -body {
        set a [ted query {SELECT * FROM orggroups_view}]

        orggroup mutate update  {
            g              USAID
            longname       ""
            color          ""
            shape          ""
            orgtype        ""
            rollup_weight  ""
            effects_factor ""
        }

        set b [ted query {SELECT * FROM orggroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.4 {<Entity> update} -setup {
        ted create USAID
    } -body {
        ted notifier bind ::orggroup <Entity>
        orggroup mutate update  {
            g              USAID
            longname       ""
            color          ""
            shape          ""
            orgtype        ""
            rollup_weight  ""
            effects_factor ""
        }
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> update USAID}
    }


    test update-2.1 {undo undoes the change} -setup {
        ted create USAID
    } -body {

        set a [ted query {SELECT * FROM orggroups_view}]

        set undo [orggroup mutate update {
            g              USAID
            longname       "Red Cross"
            color          "#ff0000"
            shape          FRIEND
            orgtype        IGO
            rollup_weight  0.1
            effects_factor 0.2
        }]

        {*}$undo

        set b [ted query {SELECT * FROM orggroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test update-2.2 {undo sends <Entity> update} -setup {
        ted create USAID
    } -body {
        set undo [orggroup mutate update {
            g              USAID
            longname       ""
            color          ""
            shape          ""
            orgtype        ""
            rollup_weight  ""
            effects_factor ""
        }]

        ted notifier bind ::orggroup <Entity>
        {*}$undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::orggroup <Entity> update USAID}
    }


    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns names: none defined} -body {
        orggroup names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns names} -setup {
        ted create USAID HAL
    } -body {
        orggroup names
    } -cleanup {
        cleanup
    } -result {USAID HAL}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid name: none defined} -body {
        orggroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid organization group, none are defined}

    test validate-1.2 {Invalid name: some defined} -setup {
        ted create USAID HAL
    } -body {
        orggroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid organization group, should be one of: USAID, HAL}

    test validate-2.1 {Valid name} -setup {
        ted create USAID HAL
    } -body {
        orggroup validate HAL
    } -cleanup {
        cleanup
    } -result {HAL}


    #-------------------------------------------------------------------
    # reconfigure

    test reconfigure-1.1 {can be called} -body {
        # This is currently a no-op
        orggroup reconfigure
    } -result {}
    


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





