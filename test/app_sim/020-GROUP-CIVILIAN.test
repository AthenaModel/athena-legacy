# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-GROUP-CIVILIAN.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) GROUP:CIVILIAN:* order tests.
#
#    This test suite tests the civgroup-related orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Standard groups

    set shia {
        g         SHIA
        longname  "Shia Locals"
        n         NB1
        color     #ffff00
        shape     NEUTRAL
        demeanor  AVERAGE
        basepop   10000
        sap       0
    }

    set sunn {
        g         SUNN
        longname  "Sunni Locals"
        n         NB1
        color     #ffffff
        shape     ENEMY
        demeanor  AGGRESSIVE
        basepop   20000
        sap       1
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:CREATE

    test CREATE-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:CREATE {}
    } -result {
        g {required value}
        longname {required value}
        n {required value}
        color {required value}
        shape {required value}
        demeanor {required value}
        basepop {required value}
        sap {required value}
    }
    
    test CREATE-1.2 {Duplicate g, longname} -setup {
        ted create NB1
        ted order GROUP:CIVILIAN:CREATE $shia
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE $shia
    } -cleanup {
        cleanup
    } -result {
        g {An entity with this ID already exists}
        longname {An entity with this name already exists}
    }

    test CREATE-1.3 {longname duplicates g} -body {
        ted create NB1
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            longname SHIA
        }]
    } -cleanup {
        cleanup
    } -result {
        longname {longname must not be identical to ID}
    }

    test CREATE-1.4 {g is not an ident} -body {
        ted create NB1
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            g "BL&E"
        }]
    } -cleanup {
        cleanup
    } -result {
        g {Identifiers begin with a letter and contain only letters and digits.}
    }

    test CREATE-1.5 {invalid n} -body {
        ted create NB1
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            n NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }
    

    test CREATE-1.6 {invalid color} -body {
        ted create NB1
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            color NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        color {Invalid hexadecimal color specifier, should be "#RRGGBB"}
    }


    test CREATE-1.7 {invalid shape} -body {
        ted create NB1
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            shape NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        shape {invalid value "NONESUCH", should be one of: FRIEND, ENEMY, NEUTRAL}
    }

    test CREATE-1.8 {invalid demeanor} -setup {
        ted create NB1
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            demeanor NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        demeanor {invalid value "NONESUCH", should be one of: APATHETIC, AVERAGE, AGGRESSIVE}
    }


    test CREATE-1.9 {non-numeric basepop} -setup {
        ted create NB1
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            basepop NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "NONESUCH", expected integer}
    }


    test CREATE-1.10 {zero basepop} -setup {
        ted create NB1
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            basepop 0
        }]
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "0", expected integer no less than 1}
    }

    test CREATE-1.11 {invalid sap 1} -setup {
        ted create NB1
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            sap NONESUCH
        }]
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "NONESUCH", expected integer}
    }

    test CREATE-1.12 {invalid sap 2} -setup {
        ted create NB1
    } -body {
        ted order -reject GROUP:CIVILIAN:CREATE [dict merge $shia {
            sap -1
        }]
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "-1", expected integer in range 0, 100}
    }

    test CREATE-2.1 {group is created} -body {
        ted create NB1
        ted order GROUP:CIVILIAN:CREATE $shia
        
        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname    color   shape   symbol   demeanor gtype n   basepop sap 
---- ----------- ------- ------- -------- -------- ----- --- ------- --- 
SHIA Shia Locals #ffff00 NEUTRAL civilian AVERAGE  CIV   NB1 10000   0   
    }


    test CREATE-2.2 {effects} -body {
        ted create NB1
        ted notifier bind ::civgroup <Entity>
        ted notifier bind ::scenario <Reconcile>
        ted order GROUP:CIVILIAN:CREATE $sunn
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> create SUNN}
        {::scenario <Reconcile>}
    }


    test CREATE-3.1 {effects are undone} -setup {
        ted create NB1
        ted order GROUP:CIVILIAN:CREATE $sunn
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> delete SUNN}
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:DELETE

    test DELETE-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:DELETE {}
    } -result {
        g {required value}
    }
    

    test DELETE-1.2 {unknown g} -body {
        ted order -reject GROUP:CIVILIAN:DELETE g NONESUCH
    } -result {
        g {Invalid civilian group, none are defined}
    }


    test DELETE-2.1 {Effects} -setup {
        ted create NB1 SHIA
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted notifier bind ::scenario <Reconcile>
        ted order GROUP:CIVILIAN:DELETE g SHIA
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> delete SHIA}
        {::scenario <Reconcile>}
    }


    test DELETE-3.1 {Effects on undo} -setup {
        ted create NB1 SHIA
        ted order GROUP:CIVILIAN:DELETE g SHIA
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> create SHIA}
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:UPDATE

    test UPDATE-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE {}
    } -result {
        g {required value}
    }

    
    test UPDATE-1.2 {Duplicate longname} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA longname "Sunni"
    } -cleanup {
        cleanup
    } -result {
        longname {An entity with this name already exists}
    }


    test UPDATE-1.3 {longname duplicates some g} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA longname SUNN
    } -cleanup {
        cleanup
    } -result {
        longname {An entity with this ID already exists}
    }

    test UPDATE-1.4 {invalid neighborhood} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }

    test UPDATE-1.5 {invalid color} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA color NONESUCH
    } -cleanup {
        cleanup
    } -result {
        color {Invalid hexadecimal color specifier, should be "#RRGGBB"}
    }


    test UPDATE-1.6 {invalid shape} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA shape NONESUCH
    } -cleanup {
        cleanup
    } -result {
        shape {invalid value "NONESUCH", should be one of: FRIEND, ENEMY, NEUTRAL}
    }


    test UPDATE-1.7 {invalid demeanor} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA demeanor NONESUCH
    } -cleanup {
        cleanup
    } -result {
        demeanor {invalid value "NONESUCH", should be one of: APATHETIC, AVERAGE, AGGRESSIVE}
    }


    test UPDATE-1.8 {invalid population 1} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE \
            g SHIA basepop NONESUCH
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "NONESUCH", expected integer}
    }


    test UPDATE-1.9 {invalid population 2} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE \
            g SHIA basepop 0
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "0", expected integer no less than 1}
    }


    test UPDATE-1.10 {invalid sap 1} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA sap NONESUCH
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "NONESUCH", expected integer}
    }


    test UPDATE-1.11 {invalid sap 2} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE g SHIA sap -1
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "-1", expected integer in range 0, 100}
    }


    test UPDATE-2.1 {civgroup is updated} -setup {
        ted create NB1 NB2 SHIA
    } -body {
        ted order GROUP:CIVILIAN:UPDATE {
            g            SHIA
            longname     "Shia Folk"
            n            NB2
            color        "#ff0000"
            shape        ENEMY
            demeanor     APATHETIC
            basepop      5000
            sap          50
        }

        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname  color   shape symbol   demeanor  gtype n   basepop sap 
---- --------- ------- ----- -------- --------- ----- --- ------- --- 
SHIA Shia Folk #ff0000 ENEMY civilian APATHETIC CIV   NB2 5000    50  
    }


    test UPDATE-2.2 {<Entity> update} -setup {
        ted create NB1 SHIA
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE g SHIA longname "Shia Folk"
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }


    test UPDATE-3.1 {undo effects} -setup {
        ted create NB1 SHIA
        ted order GROUP:CIVILIAN:UPDATE {
            g            SHIA
            longname     "Shia Folk"
            color        "#ff0000"
        }
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:UPDATE:POSTPREP

    test UPDATE:POSTPREP-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:POSTPREP {}
    } -result {
        g {required value}
    }

    
    test UPDATE:POSTPREP-1.2 {Undefined group} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:POSTPREP g SUNN sap 5
    } -cleanup {
        cleanup
    } -result {
        g {Invalid civilian group, should be one of: SHIA}
    }

    test UPDATE:POSTPREP-1.3 {invalid sap} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:POSTPREP g SHIA sap NONESUCH
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "NONESUCH", expected integer}
    }


    test UPDATE:POSTPREP-2.1 {civgroup is updated} -setup {
        ted create NB1 SHIA
    } -body {
        ted order GROUP:CIVILIAN:UPDATE:POSTPREP g SHIA sap 5

        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname color   shape   symbol   demeanor gtype n   basepop sap 
---- -------- ------- ------- -------- -------- ----- --- ------- --- 
SHIA Shia     #c00001 NEUTRAL civilian AVERAGE  CIV   NB1 1000    5   
    }

    test UPDATE:POSTPREP-2.2 {<Entity> update} -setup {
        ted create NB1 SHIA
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE:POSTPREP g SHIA sap 5
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }


    test UPDATE:POSTPREP-3.1 {undo effects} -setup {
        ted create NB1 SHIA
        ted order GROUP:CIVILIAN:UPDATE:POSTPREP g SHIA sap 5
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:UPDATE:MULTI

    test UPDATE:MULTI-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI {}
    } -result {
        ids {required value}
    }
    
    test UPDATE:MULTI-1.2 {invalid ids, no groups} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids NONESUCH
    } -result {
        ids {Invalid civilian group, none are defined}
    }

    test UPDATE:MULTI-1.3 {invalid ids, groups} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids NONESUCH
    } -cleanup {
        cleanup
    } -result {
        ids {Invalid civilian group, should be one of: SHIA}
    }

    test UPDATE:MULTI-1.4 {invalid neighborhood} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }


    test UPDATE:MULTI-1.5 {invalid color} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA color NONESUCH
    } -cleanup {
        cleanup
    } -result {
        color {Invalid hexadecimal color specifier, should be "#RRGGBB"}
    }

    test UPDATE:MULTI-1.6 {invalid shape} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA shape NONESUCH
    } -cleanup {
        cleanup
    } -result {
        shape {invalid value "NONESUCH", should be one of: FRIEND, ENEMY, NEUTRAL}
    }


    test UPDATE:MULTI-1.7 {invalid demeanor} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA demeanor NONESUCH
    } -cleanup {
        cleanup
    } -result {
        demeanor {invalid value "NONESUCH", should be one of: APATHETIC, AVERAGE, AGGRESSIVE}
    }


    test UPDATE:MULTI-1.8 {invalid population 1} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI \
            ids SHIA basepop NONESUCH
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "NONESUCH", expected integer}
    }


    test UPDATE:MULTI-1.9 {invalid population 2} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI \
            ids SHIA basepop 0
    } -cleanup {
        cleanup
    } -result {
        basepop {invalid value "0", expected integer no less than 1}
    }


    test UPDATE:MULTI-1.10 {invalid sap 1} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA sap NONESUCH
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "NONESUCH", expected integer}
    }


    test UPDATE:MULTI-1.11 {invalid sap 2} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI ids SHIA sap -1
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "-1", expected integer in range 0, 100}
    }


    test UPDATE:MULTI-2.1 {one civgroup of several is updated} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE:MULTI ids SHIA color "#ff0000"
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }


    test UPDATE:MULTI-2.2 {several civgroups are updated} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE:MULTI ids {SHIA SUNN} color "#ff0000"
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
        {::civgroup <Entity> update SUNN}
    }


    test UPDATE:MULTI-3.1 {undo effects} -setup {
        ted create NB1 SHIA SUNN
        ted order GROUP:CIVILIAN:UPDATE:MULTI ids {SHIA SUNN} color "#ff0000"
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
        {::civgroup <Entity> update SUNN}
    }

    #-------------------------------------------------------------------
    # GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP

    test UPDATE:MULTI:POSTPREP-1.1 {required parms} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP {}
    } -result {
        ids {required value}
    }
    
    test UPDATE:MULTI:POSTPREP-1.2 {invalid ids, no groups} -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP ids NONESUCH
    } -result {
        ids {Invalid civilian group, none are defined}
    }

    test UPDATE:MULTI:POSTPREP-1.3 {invalid ids, groups} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP ids NONESUCH
    } -cleanup {
        cleanup
    } -result {
        ids {Invalid civilian group, should be one of: SHIA}
    }


    test UPDATE:MULTI:POSTPREP-1.4 {invalid sap 1} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP \
            ids SHIA sap NONESUCH
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "NONESUCH", expected integer}
    }


    test UPDATE:MULTI:POSTPREP-1.5 {invalid sap 2} -setup {
        ted create NB1 SHIA
    } -body {
        ted order -reject GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP \
            ids SHIA sap -1
    } -cleanup {
        cleanup
    } -result {
        sap {invalid value "-1", expected integer in range 0, 100}
    }


    test UPDATE:MULTI:POSTPREP-2.1 {one civgroup of several is updated} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP ids SHIA sap 5
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
    }


    test UPDATE:MULTI:POSTPREP-2.2 {several civgroups are updated} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        ted notifier bind ::civgroup <Entity>
        ted order GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP \
            ids {SHIA SUNN} sap 5
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
        {::civgroup <Entity> update SUNN}
    }


    test UPDATE:MULTI:POSTPREP-3.1 {undo effects} -setup {
        ted create NB1 SHIA SUNN
        ted order GROUP:CIVILIAN:UPDATE:MULTI:POSTPREP \
            ids {SHIA SUNN} sap 5
    } -body {
        ted notifier bind ::civgroup <Entity>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::civgroup <Entity> update SHIA}
        {::civgroup <Entity> update SUNN}
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





