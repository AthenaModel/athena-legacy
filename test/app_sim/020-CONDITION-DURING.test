# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-CONDITION-DURING.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) CONDITION:DURING:* order tests.
#
#    This test suite tests the CONDITION:DURING:* orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted create JOE BOB NB1 NB2 BLUE BRIT ALQ
        ted order TACTIC:DEPOSIT:CREATE owner JOE amount 10
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard items

    set C1 {
        cc_id          1
        t1             1
        t2             10
    }

    #-------------------------------------------------------------------
    # CONDITION:DURING:CREATE

    test DURING:CREATE-1.1 {required parms} -body {
        ted order -reject CONDITION:DURING:CREATE {}
    } -result {
        cc_id {required value}
        t1 {required value}
        t2 {required value}
    }

    
    test DURING:CREATE-1.2 {tactic is unknown} -setup {
        setup
    } -body {
        ted order -reject CONDITION:DURING:CREATE \
            [dict merge $C1 [list cc_id NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        cc_id {Invalid goal or tactic ID: "NONESUCH"}
    }

    test DURING:CREATE-1.3 {t1 is not timespec} -setup {
        setup
    } -body {
        ted order -reject CONDITION:DURING:CREATE \
            [dict merge $C1 [list t1 NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        t1 {invalid time spec "NONESUCH", base time should be "NOW", "T0", an integer tick, or a week string}
    }

    test DURING:CREATE-1.4 {t2 is not timespec} -setup {
        setup
    } -body {
        ted order -reject CONDITION:DURING:CREATE \
            [dict merge $C1 [list t2 NONESUCH]]
    } -cleanup {
        cleanup
    } -result {
        t2 {invalid time spec "NONESUCH", base time should be "NOW", "T0", an integer tick, or a week string}
    }

    test DURING:CREATE-1.5 {t2 < t1} -setup {
        setup
    } -body {
        ted order -reject CONDITION:DURING:CREATE [dict merge $C1 {
            t1 5 t2 4
        }]
    } -cleanup {
        cleanup
    } -result {
        t2 {End week must be no earlier than start week.}
    }

    test DURING:CREATE-2.1 {condition is created} -setup {
        setup
    } -body {
        ted order CONDITION:DURING:CREATE $C1

        ted querylist {SELECT * FROM conditions_DURING}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  DURING
cc_id           1
narrative       The current simulation time is between week 1 and week 10, inclusive, i.e., from 2012W02 to 2012W11.
state           normal
flag            
t1              1
t2              10
    }


    test DURING:CREATE-2.2 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <conditions>
        ted order CONDITION:DURING:CREATE $C1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    test DURING:CREATE-3.1 {undo effects} -setup {
        setup
        ted order CONDITION:DURING:CREATE $C1
    } -body {
        ted notifier bind ::rdb <conditions>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> delete 1}
    }


    #-------------------------------------------------------------------
    # CONDITION:DURING:UPDATE

    proc setup_update_during {} {
        variable C1
        setup
        ted order CONDITION:DURING:CREATE $C1
    }

    test DURING:UPDATE-1.1 {required parms} -body {
        ted order -reject CONDITION:DURING:UPDATE {}
    } -result {
        condition_id {required value}
    }

    
    test DURING:UPDATE-1.2 {condition_id is unknown} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id NONESUCH t1 2
    } -cleanup {
        cleanup
    } -result {
        condition_id {Invalid condition ID: "NONESUCH"}
    }

    test DURING:UPDATE-1.3 {t1 is not timespec} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 t1 NONESUCH
    } -cleanup {
        cleanup
    } -result {
        t1 {invalid time spec "NONESUCH", base time should be "NOW", "T0", an integer tick, or a week string}
    }

    test DURING:UPDATE-1.4 {t2 is not timespec} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 t2 NONESUCH
    } -cleanup {
        cleanup
    } -result {
        t2 {invalid time spec "NONESUCH", base time should be "NOW", "T0", an integer tick, or a week string}
    }

    test DURING:UPDATE-1.5 {t2 < t1} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 \
            t1 5 t2 4
    } -cleanup {
        cleanup
    } -result {
        t2 {End week must be no earlier than start week.}
    }

    test DURING:UPDATE-1.6 {t2 < t1, t1 omitted} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 \
            t2 0
    } -cleanup {
        cleanup
    } -result {
        t2 {End week must be no earlier than start week.}
    }

    test DURING:UPDATE-1.7 {t2 < t1, t2 omitted} -setup {
        setup_update_during
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 \
            t1 20
    } -cleanup {
        cleanup
    } -result {
        t2 {End week must be no earlier than start week.}
    }

    test DURING:UPDATE-1.8 {wrong flavor of condition} -setup {
        setup_update_during
        rdb eval {UPDATE conditions SET condition_type='BOGUS'}
    } -body {
        ted order -reject CONDITION:DURING:UPDATE condition_id 1 t1 5
    } -cleanup {
        cleanup
    } -result {
        condition_id {Condition 1 is not a DURING condition}
    }

    test DURING:UPDATE-2.1 {activities are updated} -setup {
        setup_update_during
    } -body {
        ted order CONDITION:DURING:UPDATE condition_id 1 t1 2 t2 20

        ted querylist {SELECT * FROM conditions_DURING}
    } -cleanup {
        cleanup
    } -result {
condition_id    1
condition_type  DURING
cc_id           1
narrative       The current simulation time is between week 2 and week 20, inclusive, i.e., from 2012W03 to 2012W21.
state           normal
flag            
t1              2
t2              20
    }


    test DURING:UPDATE-2.2 {Effects} -setup {
        setup_update_during
    } -body {
        ted notifier bind ::rdb <conditions>
        ted order CONDITION:DURING:UPDATE condition_id 1 t1 2
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    test DURING:UPDATE-3.1 {undo effects} -setup {
        setup_update_during
        ted order CONDITION:DURING:UPDATE condition_id 1 t1 2
    } -body {
        ted notifier bind ::rdb <conditions>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <conditions> update 1}
    }


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::










