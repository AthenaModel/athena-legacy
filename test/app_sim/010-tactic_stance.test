# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_stance.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_stance(sim) tests.
#
#    This test suite tests the tactic::STANCE ensemble.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BOB NB1 NB2 BLUE BRIT SHIA SUNN KURD
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        rdb nullvalue ""
        array unset parms
        ted cleanup
    }

    set T1 {
        tactic_type STANCE
        owner       JOE
        f           BLUE
        text1       GROUP
        glist       {SUNN}
        nlist       {}
        x1          0.5
        on_lock     1
        priority    top
    }

    set T2 {
        tactic_type STANCE
        owner       JOE
        f           BLUE
        text1       GROUP
        glist       {SUNN KURD}
        nlist       {}
        x1          0.6
        on_lock     1
        priority    top
    }

    set T3 {
        tactic_type STANCE
        owner       JOE
        f           BLUE
        text1       NBHOOD
        glist       {}
        nlist       {NB1}
        x1          0.7
        on_lock     1
        priority    top
    }

    set T4 {
        tactic_type STANCE
        owner       JOE
        f           BLUE
        text1       NBHOOD
        glist       {}
        nlist       {NB1 NB2}
        x1          0.8
        on_lock     1
        priority    top
    }


    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {GROUP, one group} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::STANCE narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE adopts a stance of 0.50 (Likes) toward this group: SUNN.}

    test narrative-1.2 {GROUP, multiple groups} -setup {
        setup
        tactic mutate create $T2
    } -body {
        tactic::STANCE narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE adopts a stance of 0.60 (Likes) toward these groups: SUNN, KURD.}

    test narrative-1.3 {NBHOOD, one neighborhood} -setup {
        setup
        tactic mutate create $T3
    } -body {
        tactic::STANCE narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE adopts a stance of 0.70 (Supports) toward the civilians in this neighborhood: NB1.}

    test narrative-1.4 {NBHOOD, multiple neighborhoods} -setup {
        setup
        tactic mutate create $T4
    } -body {
        tactic::STANCE narrative [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE adopts a stance of 0.80 (Supports) toward the civilians in these neighborhoods: NB1, NB2.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {check: f no longer exists} -setup {
        setup
        tactic mutate create $T1
        rdb eval {DELETE FROM frcgroups WHERE g='BLUE'}
    } -body {
        tactic::STANCE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE does not exist, or actor JOE no longer owns it.}

    test check-1.2 {check: g in glist no longer exists} -setup {
        setup
        tactic mutate create $T2
        rdb eval {DELETE FROM groups WHERE g='SUNN'}
        rdb eval {DELETE FROM civgroups WHERE g='SUNN'}
    } -body {
        tactic::STANCE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group SUNN no longer exists.}

    test check-1.3 {check: n in nlist no longer exists} -setup {
        setup
        tactic mutate create $T4
        rdb eval {DELETE FROM nbhoods WHERE n='NB1'}
    } -body {
        tactic::STANCE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Neighborhood NB1 no longer exists.}

    test check-1.4 {check: multiple failures} -setup {
        setup
        tactic mutate create $T4
        rdb eval {UPDATE frcgroups SET a=NULL}
        rdb eval {DELETE FROM nbhoods WHERE n='NB1'}
    } -body {
        tactic::STANCE check [tactic get 1]
    } -cleanup {
        cleanup
    } -result {Group BLUE does not exist, or actor JOE no longer owns it.  Neighborhood NB1 no longer exists.}

    #-------------------------------------------------------------------
    # dollars

    test dollars-1.1 {dollars, default settings} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::STANCE dollars [tactic get 1]
    } -cleanup {
        cleanup
    } -result {n/a}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {normally successful} -setup {
        setup
        tactic mutate create $T1
    } -body {
        tactic::STANCE execute [tactic get 1]
    } -cleanup {
        cleanup
    } -result {1}

    test execute-1.2 {unsuccessful when stances already set} -setup {
        setup
        tactic mutate create $T1
        tactic mutate create $T1
    } -body {
        tactic::STANCE execute [tactic get 1]
        tactic::STANCE execute [tactic get 2]
    } -cleanup {
        cleanup
    } -result {0}

    test execute-2.1 {GROUP mode} -setup {
        setup
        tactic mutate create $T2
    } -body {
        tactic::STANCE execute [tactic get 1]
        ted query {SELECT * FROM stance_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    stance 
---- ---- ------ 
BLUE SUNN 0.6    
BLUE KURD 0.6    
    }

    test execute-2.2 {NBHOOD mode} -setup {
        setup
        tactic mutate create $T4
    } -body {
        tactic::STANCE execute [tactic get 1]
        ted query {SELECT * FROM stance_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    stance 
---- ---- ------ 
BLUE SHIA 0.8    
BLUE SUNN 0.8    
BLUE KURD 0.8    
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






