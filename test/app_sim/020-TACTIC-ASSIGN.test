# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-ASSIGN.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    TACTIC:ASSIGN:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BLUE ALQ NB1 NB2 SUNN SHIA KURD USAID
        variable S1
        variable B1
        variable T1
        set S1 [strategy getname JOE]
        ted order STRATEGY:BLOCK:ADD agent JOE
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename ASSIGN
        lassign [$B1 tactics] T1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:ASSIGN

    test TACTIC:ASSIGN-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTIC:ASSIGN-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {Invalid ASSIGN ID: "NONESUCH"}
    }

    test TACTIC:ASSIGN-1.3 {invalid g} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] g NONESUCH
    } -cleanup {
        cleanup
    } -result {
        g {invalid value "NONESUCH", should be one of: BLUE, USAID}
    }

    test TACTIC:ASSIGN-1.4 {invalid n} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1, NB2}
    }

    test TACTIC:ASSIGN-1.5 {invalid activity} -setup {
        setup
    } -body {
        $T1 set g BLUE
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] activity NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        activity {Invalid activity, should be one of: *}
    }

    test TACTIC:ASSIGN-1.6 {invalid activity for g} -setup {
        setup
    } -body {
        $T1 set g USAID
        $T1 set personnel 100
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] activity PATROL
    } -cleanup {
        cleanup
    } -match glob -result {
        activity {Invalid activity for group USAID: "PATROL"}
    }

    test TACTIC:ASSIGN-1.7 {invalid personnel} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] personnel NONESUCH
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "NONESUCH", expected integer}
    }

    test TACTIC:ASSIGN-1.8 {0 personnel} -setup {
        setup
    } -body {
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] personnel 0
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "0", expected integer no less than 1}
    }

    test TACTIC:ASSIGN-1.9 {personnel is 0} -setup {
        setup
    } -body {
        $T1 set g BLUE
        $T1 set n NB1
        $T1 set activity PATROL
        ted order -reject TACTIC:ASSIGN tactic_id [$T1 id] 
    } -cleanup {
        cleanup
    } -result {
        personnel {Personnel must be positive}
    }

    test TACTIC:ASSIGN-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTIC:ASSIGN \
            tactic_id [$T1 id] \
            g         BLUE     \
            n         NB1      \
            activity  PATROL   \
            personnel 1000

        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match glob -result {
        activity   PATROL
        execstatus NONE
        g          BLUE
        id         *
        n          NB1
        parent     ::bean::block*
        personnel  1000
        state      normal
    }

    test TACTIC:ASSIGN-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:ASSIGN tactic_id [$T1 id] \
            g         BLUE     \
            n         NB1      \
            activity  PATROL   \
            personnel 1000
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:ASSIGN-2.3 {undo notifier event} -setup {
        setup
        ted order TACTIC:ASSIGN tactic_id [$T1 id] \
            g         BLUE     \
            n         NB1      \
            activity  PATROL   \
            personnel 1000
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







