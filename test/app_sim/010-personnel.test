# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-personnel.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    personnel(sim) tests.
#
#    This test suite tests the personnel(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    proc setup {} {
        ted create NB1 NB2 BLUE BRIT HAL
    }

    #-------------------------------------------------------------------
    # start

    test start-1.2 {start populates personnel_g} -setup {
        setup
    } -body {
        personnel start
        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5000      0         
BRIT 5000      0         
HAL  2000      0         
    }

    test start-1.2 {start populates deploy_ng} -setup {
        setup
    } -body {
        personnel start
        ted query {SELECT * FROM deploy_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 0         0          
NB2 BLUE 0         0          
NB1 BRIT 0         0          
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }

    #-------------------------------------------------------------------
    # reset

    test reset-1.1 {resets personnel_g} -setup {
        setup
        personnel start
    } -body {
        personnel reset
        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5000      5000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    test reset-1.2 {resets deploy_ng} -setup {
        setup
        personnel start
        ted deploy NB1 BLUE 100
        ted deploy NB1 BRIT 100
    } -body {
        personnel reset
        ted query {SELECT * FROM deploy_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 0         0          
NB2 BLUE 0         0          
NB1 BRIT 0         0          
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }


    #-------------------------------------------------------------------
    # deploy

    test deploy-1.1 {fails if troops are unavailable} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel deploy NB1 BLUE 6000
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Insufficient personnel available: 6000 > 5000}

    test deploy-2.1 {Decrements available} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel deploy NB1 BLUE 1000
        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5000      4000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    test deploy-2.2 {Increments personnel} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel deploy NB1 BLUE 1000
        ted query {SELECT * FROM deploy_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 1000      1000       
NB2 BLUE 0         0          
NB1 BRIT 0         0          
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }

    test deploy-2.3 {Multiple deployments to same n,g} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel deploy NB1 BLUE 1000
        personnel deploy NB1 BLUE 500
        ted query {SELECT * FROM deploy_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 1500      1500       
NB2 BLUE 0         0          
NB1 BRIT 0         0          
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }

    #-------------------------------------------------------------------
    # demob

    test demob-1.1 {fails if troops are unavailable} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel demob BLUE 6000
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Insufficient personnel available: 6000 > 5000}

    test demob-2.1 {Decrements personnel and available} -setup {
        setup
        personnel start
        personnel reset
    } -body {
        personnel demob BLUE 1000
        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 4000      4000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    #-------------------------------------------------------------------
    # mutate attrit

    test attrit-1.1 {attrit updates the personnel_g count} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
    } -body {
        personnel mutate attrit NB1 BLUE 15
        ted query {SELECT * FROM personnel_g ORDER BY g}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 4985      4900      
BRIT 5000      5000      
HAL  2000      2000      
    }

    test attrit-1.2 {attrit updates the deploy_ng count} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        personnel mutate attrit NB1 BLUE 15
        ted query {SELECT * FROM deploy_ng ORDER BY g}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 85        100        
NB2 BLUE 0         0          
NB1 BRIT 100       100        
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }

    test attrit-1.3 {attrits down to zero} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        personnel mutate attrit NB1 BLUE 150

        set personnel [rdb onecolumn {
            SELECT personnel FROM personnel_g WHERE g='BLUE'
        }]

        set deployed [rdb onecolumn {
            SELECT personnel FROM deploy_ng
            WHERE n='NB1' AND g='BLUE'
        }]

        list $personnel $deployed
    } -cleanup {
        cleanup
    } -result {4900 0}


    test attrit-2.1 {undo undoes the personnel_g change} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        set a [ted query {SELECT * FROM personnel_g}]
        set undo [personnel mutate attrit NB1 BLUE 30]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM personnel_g}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.2 {undo undoes the deploy_ng change} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [personnel mutate attrit NB1 BLUE 30]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.3 {undo restores truncated value} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [personnel mutate attrit NB1 BLUE 150]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.4 {joined undo's yield the right answer} -setup {
        setup
        personnel start
        personnel reset
        personnel deploy NB1 BLUE 100
        personnel deploy NB1 BRIT 100
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [list]
        lappend undo [personnel mutate attrit NB1 BLUE 30]
        lappend undo [personnel mutate attrit NB1 BLUE 15]
        namespace eval :: [join $undo \n]
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






