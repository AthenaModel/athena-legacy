# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-personnel.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    personnel(sim) tests.
#
#    This test suite tests the personnel(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    proc setup {} {
        ted create NB1 NB2 BLUE BRIT HAL
    }

    #-------------------------------------------------------------------
    # start

    test start-1.1 {start populates personnel_g} -setup {
        setup
    } -body {
        personnel start
        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel 
---- --------- 
BLUE 5000      
BRIT 5000      
HAL  2000      
    }

    test start-1.2 {start populates deploy_ng} -setup {
        setup
    } -body {
        personnel start
        ted query {SELECT * FROM deploy_ng}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 0         0          
NB1 BRIT 0         0          
NB1 HAL  0         0          
NB2 BLUE 0         0          
NB2 BRIT 0         0          
NB2 HAL  0         0          
    }

    #-------------------------------------------------------------------
    # load

    test load-1.1 {loads working_personnel} -setup {
        setup
        personnel start
    } -body {
        personnel load
        ted query {SELECT * FROM working_personnel}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5000      5000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    test load-1.2 {loads working_deployment} -setup {
        setup
        personnel start
    } -body {
        personnel load
        ted query {SELECT * FROM working_deployment}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 0         0          
NB1 BRIT 0         0          
NB1 HAL  0         0          
NB2 BLUE 0         0          
NB2 BRIT 0         0          
NB2 HAL  0         0          
    }


    #-------------------------------------------------------------------
    # deploy

    test deploy-1.1 {fails if troops are unavailable} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel deploy 1 NB1 BLUE 6000
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Insufficient personnel available: 6000 > 5000}

    test deploy-2.1 {Decrements available} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel deploy 1 NB1 BLUE 1000
        ted query {SELECT * FROM working_personnel}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 5000      4000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    test deploy-2.2 {Increments personnel} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel deploy 1 NB1 BLUE 1000
        ted query {SELECT * FROM working_deployment}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 1000      1000       
NB1 BRIT 0         0          
NB1 HAL  0         0          
NB2 BLUE 0         0          
NB2 BRIT 0         0          
NB2 HAL  0         0          
    }

    test deploy-2.3 {Multiple deployments to same n,g} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel deploy 1 NB1 BLUE 1000
        personnel deploy 2 NB1 BLUE 500
        ted query {SELECT * FROM working_deployment}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 1500      1500       
NB1 BRIT 0         0          
NB1 HAL  0         0          
NB2 BLUE 0         0          
NB2 BRIT 0         0          
NB2 HAL  0         0          
    }

    #-------------------------------------------------------------------
    # demob

    test demob-1.1 {fails if troops are unavailable} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel demob BLUE 6000
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Insufficient personnel available: 6000 > 5000}

    test demob-2.1 {Decrements personnel and available} -setup {
        setup
        personnel start
        personnel load
    } -body {
        personnel demob BLUE 1000
        ted query {SELECT * FROM working_personnel}
    } -cleanup {
        cleanup
    } -result {
g    personnel available 
---- --------- --------- 
BLUE 4000      4000      
BRIT 5000      5000      
HAL  2000      2000      
    }

    #-------------------------------------------------------------------
    # save

    test save-1.1 {Logs changed deployments} -setup {
        setup
        personnel start
        personnel load

        # Initially, all troops are deployed in NB1.  Move or
        # otherwise change them.
        personnel deploy 1 NB1 BLUE 2000
        personnel deploy 2 NB1 BRIT 2000
        personnel deploy 3 NB1 HAL 1000
    } -body {
        personnel save

        # Should be 6 messages: 3 deployments to NB1
        # and 3 demobilization of unused troops
        rdb eval {SELECT count(event_id) FROM sigevents}
    } -cleanup {
        cleanup
    } -result {6}

    test save-1.2 {Saves personnel back to personnel_g} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 2000
        personnel deploy 2 NB1 BRIT 5000
        personnel deploy 3 NB1 HAL 2000
    } -body {
        personnel save

        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel 
---- --------- 
BLUE 2000      
BRIT 5000      
HAL  2000      
    }

    test save-1.3 {Automatically demobilizes undeployed personnel} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 2000
    } -body {
        personnel save

        ted query {SELECT * FROM personnel_g}
    } -cleanup {
        cleanup
    } -result {
g    personnel 
---- --------- 
BLUE 2000      
BRIT 0         
HAL  0         
    }

    #-------------------------------------------------------------------
    # available

    test available-1.1 {Returns available} -setup {
        setup
        personnel start
        personnel load
    } -body {
        set a [personnel available BLUE]
        personnel deploy 1 NB1 BLUE 1000
        set b [personnel available BLUE]

        list $a $b
    } -cleanup {
        cleanup
    } -result {5000 4000}





    #-------------------------------------------------------------------
    # mutate attrit

    test attrit-1.1 {attrit updates the personnel_g count} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel save
    } -body {
        personnel mutate attrit NB1 BLUE 15
        ted query {SELECT * FROM personnel_g ORDER BY g}
    } -cleanup {
        cleanup
    } -result {
g    personnel 
---- --------- 
BLUE 85        
BRIT 0         
HAL  0         
    }

    test attrit-1.2 {attrit updates the deploy_ng count} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        personnel mutate attrit NB1 BLUE 15
        ted query {SELECT * FROM deploy_ng ORDER BY g}
    } -cleanup {
        cleanup
    } -result {
n   g    personnel unassigned 
--- ---- --------- ---------- 
NB1 BLUE 85        100        
NB2 BLUE 0         0          
NB1 BRIT 100       100        
NB2 BRIT 0         0          
NB1 HAL  0         0          
NB2 HAL  0         0          
    }

    test attrit-1.3 {attrits down to zero} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 1 NB2 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        personnel mutate attrit NB1 BLUE 150

        set personnel [rdb onecolumn {
            SELECT personnel FROM personnel_g WHERE g='BLUE'
        }]

        set deployed [rdb onecolumn {
            SELECT personnel FROM deploy_ng
            WHERE n='NB1' AND g='BLUE'
        }]

        list $personnel $deployed
    } -cleanup {
        cleanup
    } -result {100 0}


    test attrit-2.1 {undo undoes the personnel_g change} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        set a [ted query {SELECT * FROM personnel_g}]
        set undo [personnel mutate attrit NB1 BLUE 30]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM personnel_g}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.2 {undo undoes the deploy_ng change} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [personnel mutate attrit NB1 BLUE 30]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.3 {undo restores truncated value} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [personnel mutate attrit NB1 BLUE 150]
        namespace eval :: $undo
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    test attrit-2.4 {joined undo's yield the right answer} -setup {
        setup
        personnel start
        personnel load
        personnel deploy 1 NB1 BLUE 100
        personnel deploy 2 NB1 BRIT 100
        personnel save
    } -body {
        set a [ted query {SELECT * FROM deploy_ng}]
        set undo [list]
        lappend undo [personnel mutate attrit NB1 BLUE 30]
        lappend undo [personnel mutate attrit NB1 BLUE 15]
        namespace eval :: [join $undo \n]
        set b [ted query {SELECT * FROM deploy_ng}]
        
        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::

