# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTICX-DEMOB.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    TACTICX:DEMOB:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BLUE ALQ
        variable S1
        variable B1
        variable T1
        set S1 [strategyx getname JOE]
        ted order STRATEGY:BLOCK:ADD agent JOE
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename DEMOB
        lassign [$B1 tactics] T1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTICX:DEMOB:UPDATE

    test TACTICX:DEMOB:UPDATE-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTICX:DEMOB:UPDATE
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTICX:DEMOB:UPDATE-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTICX:DEMOB:UPDATE tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {invalid value "NONESUCH", should be one of: *}
    }

    test TACTICX:DEMOB:UPDATE-1.3 {invalid g} -setup {
        setup
    } -body {
        ted order -reject TACTICX:DEMOB:UPDATE tactic_id [$T1 id] g NONESUCH
    } -cleanup {
        cleanup
    } -result {
        g {invalid value "NONESUCH", should be one of: BLUE}
    }

    test TACTICX:DEMOB:UPDATE-1.4 {invalid personnel} -setup {
        setup
    } -body {
        ted order -reject TACTICX:DEMOB:UPDATE tactic_id [$T1 id] personnel NONESUCH
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "NONESUCH", expected integer}
    }

    test TACTICX:DEMOB:UPDATE-1.5 {0 personnel} -setup {
        setup
    } -body {
        ted order -reject TACTICX:DEMOB:UPDATE tactic_id [$T1 id] personnel 0
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "0", expected integer no less than 1}
    }

    test TACTICX:DEMOB:UPDATE-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTICX:DEMOB:UPDATE \
            tactic_id [$T1 id]         \
            g         BLUE             \
            mode      SOME             \
            personnel 1000

        ted pdict [$T1 getdict]
    } -cleanup {
        cleanup
    } -match glob -result {
        id         *
        execstatus NONE
        state      normal
        mode       SOME
        personnel  1000
        block      ::bean::block*
        g          BLUE
    }

    test TACTICX:DEMOB:UPDATE-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTICX:DEMOB:UPDATE tactic_id [$T1 id] personnel 100
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTICX:DEMOB:UPDATE-2.3 {undo notifier event} -setup {
        setup
        ted order TACTICX:DEMOB:UPDATE tactic_id [$T1 id] personnel 100
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





