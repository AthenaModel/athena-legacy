# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-sat.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    sat(sim) tests.
#
#    This test suite tests the sat(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate reconcile

    test reconcile-1.1 {nbgroup curves are populated} -setup {
        ted create NB1 SHIA SUNN
    } -body {
        sat mutate reconcile
        ted query {SELECT * FROM sat_gc}
    } -cleanup {
        cleanup
    } -result {
g    c   sat0 saliency atrend athresh dtrend dthresh 
---- --- ---- -------- ------ ------- ------ ------- 
SHIA AUT 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA SFT 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA CUL 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA QOL 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN AUT 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN SFT 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN CUL 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN QOL 0.0  1.0      0.0    0.0     0.0    0.0     
    }

    test reconcile-1.3 {Created curves are undone} -setup {
        ted create NB1 SHIA
        set undo [sat mutate reconcile]
    } -body {
        eval $undo
        rdb query {SELECT * FROM sat_gc}
    } -cleanup {
        cleanup
    } -result {}
    

    test reconcile-2.1 {CIV curves deleted with group} -setup {
        ted create NB1 SHIA SUNN
        sat mutate reconcile
    } -body {
        civgroup mutate delete SHIA
        sat mutate reconcile

        # SUNN remains
        ted query {SELECT * FROM sat_gc}
    } -cleanup {
        cleanup
    } -result {
g    c   sat0 saliency atrend athresh dtrend dthresh 
---- --- ---- -------- ------ ------- ------ ------- 
SUNN AUT 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN SFT 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN CUL 0.0  1.0      0.0    0.0     0.0    0.0     
SUNN QOL 0.0  1.0      0.0    0.0     0.0    0.0     
    }


    test reconcile-2.3 {On undo: deleted curves are restored} -setup {
        # FIRST, create the groups and related curves
        ted create NB1 NB2 SHIA KURD
        sat mutate reconcile

        # NEXT, update one, so that we can see that it's restored
        sat mutate update \
            [list id {SHIA AUT} sat0 1.0 trend0 2.0 saliency 0.5]

        # NEXT, delete the groups and related curves, and get the
        # undo script.
        civgroup mutate delete SHIA

        set undo [sat mutate reconcile]
    } -body {
        eval $undo
        ted query {SELECT * FROM sat_gc}
    } -cleanup {
        cleanup
    } -result {
g    c   sat0 saliency atrend athresh dtrend dthresh 
---- --- ---- -------- ------ ------- ------ ------- 
KURD AUT 0.0  1.0      0.0    0.0     0.0    0.0     
KURD SFT 0.0  1.0      0.0    0.0     0.0    0.0     
KURD CUL 0.0  1.0      0.0    0.0     0.0    0.0     
KURD QOL 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA AUT 1.0  0.5      0.0    0.0     0.0    0.0     
SHIA SFT 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA CUL 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA QOL 0.0  1.0      0.0    0.0     0.0    0.0     
    }


    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {sat is updated} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
    } -body {
        sat mutate update {
            id             {SHIA AUT}
            sat0           1.0
            saliency       0.5
            atrend         2.0
            athresh        -10.0
            dtrend         -2.0
            dthresh        10.0
        }

        ted query {SELECT * FROM sat_gc}
    } -cleanup {
        cleanup
    } -result {
g    c   sat0 saliency atrend athresh dtrend dthresh 
---- --- ---- -------- ------ ------- ------ ------- 
SHIA AUT 1.0  0.5      2.0    -10.0   -2.0   10.0    
SHIA SFT 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA CUL 0.0  1.0      0.0    0.0     0.0    0.0     
SHIA QOL 0.0  1.0      0.0    0.0     0.0    0.0     
    }


    test update-1.2 {identical values are OK} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
        set parmdict {
            id             {SHIA AUT}
            sat0           1.0
            saliency       0.5
            atrend         2.0
            athresh        -10.0
            dtrend         -2.0
            dthresh        10.0
        }

        sat mutate update $parmdict
    } -body {

        set a [ted query {SELECT * FROM sat_gc}]
        
        sat mutate update $parmdict

        set b [ted query {SELECT * FROM sat_gc}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
    } -body {
        set a [ted query {SELECT * FROM sat_gc}]

        sat mutate update  {
            id             {SHIA SFT}
            sat0           ""
            saliency       ""
            atrend         ""
            athresh        ""
            dtrend         ""
            dthresh        ""
        }

        set b [ted query {SELECT * FROM sat_gc}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
    } -body {

        set a [ted query {SELECT * FROM sat_gc ORDER BY g,c}]

        set undo [sat mutate update {
            id             {SHIA SFT}
            sat0           1.0
            saliency       0.5
            atrend         2.0
            athresh        -10.0
            dtrend         -2.0
            dthresh        10.0
        }]

        eval $undo

        set b [ted query {SELECT * FROM sat_gc ORDER BY g,c}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # exists

    test exists-1.1 {sat does not exist} -body {
        sat exists NONESUCH NONESUCH
    } -cleanup {
        cleanup
    } -result {0}

    test exists-1.2 {sat exists} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
    } -body {
        sat exists SHIA SFT
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid civgroup} -setup {
        ted create NB1 SHIA
    } -body {
        sat validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid civilian group, should be one of: SHIA}

    test validate-1.2 {Invalid concern} -setup {
        ted create NB1 SHIA
    } -body {
        sat validate {SHIA NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {invalid value "NONESUCH", should be one of: AUT, SFT, CUL, QOL}

    test validate-2.1 {Valid group} -setup {
        ted create NB1 SHIA
        sat mutate reconcile
    } -body {
        sat validate {SHIA SFT}
    } -cleanup {
        cleanup
    } -result {SHIA SFT}

    
    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





