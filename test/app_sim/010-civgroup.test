# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-civgroup.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    civgroup(sim) tests.
#
#    This test suite tests the civgroup(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {civgroup is created} -body {
        civgroup mutate create [ted entity SHIA]
        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname color   shape   symbol   demeanor gtype n   basepop sap 
---- -------- ------- ------- -------- -------- ----- --- ------- --- 
SHIA Shia     #c00001 NEUTRAL civilian AVERAGE  CIV   NB1 1000    10  
    }

    test create-1.2 {bsystem entity is created} -body {
        civgroup mutate create [ted entity SHIA]
        bsystem entity names
    } -cleanup {
        cleanup
    } -result {SHIA}

    test create-1.3 {sat curves are created} -body {
        civgroup mutate create [ted entity SHIA]
        rdb eval {SELECT c FROM sat_gc WHERE g='SHIA' ORDER BY c}
    } -cleanup {
        cleanup
    } -result {AUT CUL QOL SFT}

    test create-2.1 {undo deletes the civgroup} -setup {
        civgroup mutate create [ted entity SHIA]
        set undo [civgroup mutate create [ted entity SUNN]]
    } -body {
        {*}$undo
        rdb eval {SELECT g FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {SHIA}

    test create-2.2 {undo deletes the bsystem entity} -setup {
        civgroup mutate create [ted entity SHIA]
        set undo [civgroup mutate create [ted entity SUNN]]
    } -body {
        {*}$undo
        bsystem entity names
    } -cleanup {
        cleanup
    } -result {SHIA}

    test create-2.3 {undo deletes the sat curves} -setup {
        set undo [civgroup mutate create [ted entity SUNN]]
    } -body {
        {*}$undo
        rdb eval {SELECT c FROM sat_gc WHERE g='SHIA' ORDER BY c}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Group is deleted} -setup {
        ted create SHIA SUNN
    } -body {
        civgroup mutate delete SHIA

        # Is SHIA gone?
        rdb eval {SELECT g FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {SUNN}


    test delete-1.2 {Bsystem entity is deleted} -setup {
        ted create SHIA SUNN
    } -body {
        civgroup mutate delete SHIA

        # Is SHIA gone?
        bsystem entity names
    } -cleanup {
        cleanup
    } -result {SUNN}


    test delete-1.3 {sat curves are deleted} -setup {
        ted create SHIA
    } -body {
        civgroup mutate delete SHIA

        rdb eval {SELECT c FROM sat_gc WHERE g='SHIA' ORDER BY c}
    } -cleanup {
        cleanup
    } -result {}


    test delete-2.1 {Undo restores group} -setup {
        ted create SHIA
        set undo [civgroup mutate delete SHIA]
    } -body {
        {*}$undo
        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname color   shape   symbol   demeanor gtype n   basepop sap 
---- -------- ------- ------- -------- -------- ----- --- ------- --- 
SHIA Shia     #c00001 NEUTRAL civilian AVERAGE  CIV   NB1 1000    10  
    }

    test delete-2.2 {Undo restores bsystem entity} -setup {
        ted create SHIA SUNN
        set undo [civgroup mutate delete SHIA]
    } -body {
        {*}$undo
        bsystem entity names
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}

    test delete-2.3 {Undo restores sat curves} -setup {
        ted create SHIA
        set undo [civgroup mutate delete SHIA]
    } -body {
        {*}$undo
        rdb eval {SELECT c FROM sat_gc WHERE g='SHIA' ORDER BY c}
    } -cleanup {
        cleanup
    } -result {AUT CUL QOL SFT}

    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {civgroup is updated} -setup {
        ted create SHIA
    } -body {
        civgroup mutate update {
            g            SHIA
            longname     "Shia Folk"
            color        "#ff0000"
            shape        FRIEND
        }

        ted query {SELECT * FROM civgroups_view}
    } -cleanup {
        cleanup
    } -result {
g    longname  color   shape  symbol   demeanor gtype n   basepop sap 
---- --------- ------- ------ -------- -------- ----- --- ------- --- 
SHIA Shia Folk #ff0000 FRIEND civilian AVERAGE  CIV   NB1 1000    10  
    }


    test update-1.2 {identical values are OK} -setup {
        ted create SHIA
    } -body {
        set a [ted query {SELECT * FROM civgroups_view}]

        civgroup mutate update [ted entity SHIA]

        set b [ted query {SELECT * FROM civgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {empty values are OK} -setup {
        ted create SHIA
    } -body {
        set a [ted query {SELECT * FROM civgroups_view}]

        civgroup mutate update  {
            g            SHIA
            longname     ""
            color        ""
        }

        set b [ted query {SELECT * FROM civgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create SHIA
    } -body {

        set a [ted query {SELECT * FROM civgroups_view}]

        set undo [civgroup mutate update {
            g            SHIA
            longname     "Shia Folk"
            color        "#ff0000"
        }]

        {*}$undo

        set b [ted query {SELECT * FROM civgroups_view}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns names: none defined} -body {
        civgroup names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns names} -setup {
        ted create SHIA SUNN
    } -body {
        civgroup names
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid name: none defined} -body {
        civgroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid civilian group, none are defined}

    test validate-1.2 {Invalid name: some defined} -setup {
        ted create SHIA SUNN
    } -body {
        civgroup validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid civilian group, should be one of: SHIA, SUNN}

    test validate-2.1 {Valid name} -setup {
        ted create SHIA SUNN
    } -body {
        civgroup validate SUNN
    } -cleanup {
        cleanup
    } -result {SUNN}

    #-------------------------------------------------------------------
    # gInN

    test gInN-1.1 {g resides in n} -setup {
        ted create NB1 NB2 SHIA KURD
    } -body {
        civgroup gInN SHIA NB1
    } -cleanup {
        cleanup
    } -result {1}

    test gInN-1.1 {g does not reside in n} -setup {
        ted create NB1 NB2 SHIA KURD
    } -body {
        civgroup gInN KURD NB1
    } -cleanup {
        cleanup
    } -result {0}

    #-------------------------------------------------------------------
    # gIn

    test gIn-1.1 {get groups in n} -setup {
        ted create NB1 NB2 SHIA SUNN KURD
    } -body {
        civgroup gIn NB1
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}


    #-------------------------------------------------------------------
    # getg

    test getg-1.1 {get group dict} -setup {
        ted create NB1 SHIA
    } -body {
        civgroup getg SHIA
    } -cleanup {
        cleanup
    } -result "sap 10 gtype CIV shape NEUTRAL longname Shia basepop 1000 n NB1 symbol civilian demeanor AVERAGE color #c00001 g SHIA"

    test getg-1.2 {get group attribute} -setup {
        ted create NB1 SHIA
    } -body {
        civgroup getg SHIA n
    } -cleanup {
        cleanup
    } -result {NB1}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





