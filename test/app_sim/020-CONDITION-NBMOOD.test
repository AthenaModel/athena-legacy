# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-CONDITION-NBMOOD.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    CONDITION:NBMOOD:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    set S1 ::strategy::SYSTEM

    proc setup {} {
        ted create NB1
        variable S1
        variable B1
        variable C1
        ted order STRATEGY:BLOCK:ADD agent SYSTEM
        lassign [$S1 blocks] B1
        ted order BLOCK:CONDITION:ADD block_id [$B1 id] typename NBMOOD
        lassign [$B1 conditions] C1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # CONDITION:NBMOOD

    test CONDITION:NBMOOD-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject CONDITION:NBMOOD
    } -cleanup {
        cleanup
    } -result {
        condition_id {required value}
    }

    test CONDITION:NBMOOD-1.2 {unknown condition_id} -setup {
        setup
    } -body {
        ted order -reject CONDITION:NBMOOD condition_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        condition_id {invalid value "NONESUCH", should be one of: *}
    }

    test CONDITION:NBMOOD-1.3 {invalid n} -setup {
        setup
    } -body {
        ted order -reject CONDITION:NBMOOD condition_id [$C1 id] n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }

    test CONDITION:NBMOOD-1.4 {invalid comp} -setup {
        setup
    } -body {
        ted order -reject CONDITION:NBMOOD condition_id [$C1 id] comp NONESUCH
    } -cleanup {
        cleanup
    } -result {
        comp {invalid value "NONESUCH", should be one of: EQ, GE, GT, LE, LT}
    }

    test CONDITION:NBMOOD-1.5 {invalid limit} -setup {
        setup
    } -body {
        ted order -reject CONDITION:NBMOOD condition_id [$C1 id] limit NONESUCH
    } -cleanup {
        cleanup
    } -result {
        limit {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: VS, S, A, D, VD}
    }

    test CONDITION:NBMOOD-2.1 {condition updated} -setup {
        setup
    } -body {
        ted order CONDITION:NBMOOD \
            condition_id [$C1 id] \
            n            NB1     \
            comp         gt       \
            limit        10

        ted pdicts [$C1 getdict]
    } -cleanup {
        cleanup
    } -match glob -result {
        comp    GT
        id      *
        limit   10
        metflag {}
        n       NB1
        parent  ::bean::block*
        state   normal
    }

    test CONDITION:NBMOOD-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order CONDITION:NBMOOD condition_id [$C1 id] n NB1
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test CONDITION:NBMOOD-2.3 {undo notifier event} -setup {
        setup
        ted order CONDITION:NBMOOD condition_id [$C1 id] n NB1
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





