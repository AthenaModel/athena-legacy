# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-condition_unmet.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    condition_unmet(sim) tests.
#
#    This test suite tests the UNMET condition ensemble.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BOB NB1 NB2 BLUE BRIT

        # goal_ids: 1, 2, 3
        ted order GOAL:CREATE owner JOE narrative "Test Goal 1"
        ted order GOAL:CREATE owner JOE narrative "Test Goal 2"
        ted order GOAL:CREATE owner JOE narrative "Test Goal 3"

        rdb eval {
            UPDATE goals
            SET flag=1 WHERE goal_id=1;

            UPDATE goals
            SET flag=1 WHERE goal_id=2;

            UPDATE goals
            SET flag=0 WHERE goal_id=3;

        }

        # tactic_id: 4
        ted order TACTIC:SAVE:CREATE owner JOE int1 10
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    # Standard Conditions
    
    set C1 {
        cc_id          4
        condition_type UNMET
        list1          {1}
    }

    set C2 {
        cc_id          4
        condition_type UNMET
        list1          {1 2}
    }

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, one goal} -setup {
        setup
        condition mutate create $C1
    } -body {
        condition::UNMET narrative [condition get 1]
    } -cleanup {
        cleanup
    } -result {Goal is unmet:
    (1) Test Goal 1}

    test narrative-1.2 {narrative, multiple goals} -setup {
        setup
        condition mutate create $C2
    } -body {
        condition::UNMET narrative [condition get 1]
    } -cleanup {
        cleanup
    } -result {Any of these goals are unmet:
    (1) Test Goal 1
    (2) Test Goal 2}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {check: successful} -setup {
        setup
        condition mutate create $C2
    } -body {
        condition::UNMET check [condition get 1]
    } -cleanup {
        cleanup
    } -result {}

    test check-1.2 {check: goal deleted} -setup {
        setup
        condition mutate create $C2
        rdb eval {DELETE FROM goals}
    } -body {
        condition::UNMET check [condition get 1]
    } -cleanup {
        cleanup
    } -result {Goal 1 no longer exists. Goal 2 no longer exists.}

    #-------------------------------------------------------------------
    # eval

    # callEval flagdict
    #
    # flagdict   - A dictionary of goal IDs and flags
    #
    # Sets the goal flags in the goals table to match the
    # the flagdict.  Updates condition 1 to the same goals.
    # Evaluates the condition, and returns the result.

    proc callEval {flagdict} {
        dict for {gid flag} $flagdict {
            # Convert flags to integers
            if {$flag ne ""} {
                if {$flag} {
                    set flag 1
                } else {
                    set flag 0
                }
            }

            rdb eval {UPDATE goals SET flag=$flag WHERE goal_id=$gid}
        }

        condition mutate update \
            [list condition_id 1 list1 [dict keys $flagdict]]

        return [condition::UNMET eval [condition get 1]]
    }

    test eval-1.1 {eval cases} -setup {
        setup
        condition mutate create $C1
    } -body {
        set results [list]

        # FIRST, normal cases
        lappend results [callEval {1 true}]
        lappend results [callEval {1 true  2 true   3 true}]
        lappend results [callEval {1 true  2 false  3 true}]

        # NEXT, empty flags
        lappend results [callEval {1 ""}]

        set results
    } -cleanup {
        cleanup
    } -result {0 0 1 0}


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







